LABELS	     PROGRAM				  !THIS PROGRAM PRINTS TWO
						  !	ACROSS LABELS
PHONEBK	     FILE,PRE(PHN),CREATE,RECLAIM
BY_NAME	       KEY(PHN:NAME),DUP,NOCASE
BY_COMPANY     KEY(PHN:COMPANY),DUP,NOCASE
RECORD	       RECORD
NAME		 STRING(30)
COMPANY		 STRING(30)
ADDRESS		 STRING(30)
CITY		 STRING(28)
STATE		 STRING(2)
ZIP		 STRING(6)
PHONE		 DECIMAL(10,0)
	     . .


REPORT	     REPORT	  WIDTH(80),PRE(RPT)
Detail			  DETAIL
COMPANY1       ROW(+1,1)    STRING(30)
COMPANY2	 COL(45)    STRING(30)
NAME1	       ROW(+1,1)    STRING(30)
NAME2		 COL(45)    STRING(30)
ADDRESS1       ROW(+1,1)    STRING(30)
ADDRESS2	 COL(45)    STRING(30)
CSZ1	       ROW(+1,1)    STRING(35)
CSZ2		 COL(45)    STRING(35) CTL(@LF2)
	     .		  .


    CODE
    OPEN(PHONEBK)				  !OPEN PHONE BOOK FILE
    SET(PHN:BY_COMPANY)				  !PROCESS FILE IN COMPANY ORDER
    OPEN(REPORT)				  !OPEN REPORT
    LOOP UNTIL EOF(PHONEBK)			  !LOOP THRU PHONE RECORDS
      IF KEYBOARD()				  !IF A KEY WAS PRESSED
	ASK					  !READ THE KEY
	IF KEYCODE() = 256 OR KEYCODE() = 279	  !IF KEY WAS ESC OR CTRL-ESC
	  BREAK					  !BREAK OUT OF MAIN LOOP
      . .					  !END KEYBOARD() AND KEYCODE()
      LOOP I# = 1 TO 2				  !LOOP BY # OF LABELS ACROSS
	IF EOF(PHONEBK) THEN BREAK.		  !CHECK FOR END OF FILE
	NEXT(PHONEBK)				  !READ THE RECORD
	CASE I#					  !WHICH LABEL?
	  OF 1
	    RPT:COMPANY1 = PHN:COMPANY		  !MOVE DATA TO APPROPIATE LABEL
	    RPT:NAME1 = 'Attn:  ' & PHN:NAME
	    RPT:ADDRESS1 = PHN:ADDRESS
	    RPT:CSZ1 = CLIP(PHN:CITY) & ', ' & PHN:STATE & ' ' & PHN:ZIP
	  OF 2
	    RPT:COMPANY2 = PHN:COMPANY
	    RPT:NAME2 = 'Attn:  ' &  PHN:NAME
	    RPT:ADDRESS2 = PHN:ADDRESS
	    RPT:CSZ2 = CLIP(PHN:CITY) & ', ' & PHN:STATE & ' ' & PHN:ZIP
	.					  !END CASE
      .						  !END # OF LABELS LOOP
      PRINT(RPT:DETAIL)				  !PRINT LABELS
      CLEAR(RPT:DETAIL)				  !CLEAR OLD INFO
    .						  !END MAIN LOOP
    CLOSE(REPORT)				  !CLOSE THE REPORT
