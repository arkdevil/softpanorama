--
-- MacroBat: меню принтера и печати
-- Copyright (c) 1992 L.G.Bunich, Russia, Moscow
--
  H=Upper("%File(NC h)")
  Cls;  Cursor Off;  N = 1
  Box 13,61 single at 6,12 white on blue; L = Cline()
  Color Cyan+
  put %L,24 " В ы б е р и т е   ф у н к ц и ю: "
  put +2,16 "C"  "...."
  put +1    "D"  "...."
  put +1    "L"  "...."
  put +1    "N"  "...."
  put +1    "R"  "...."
  put +1,12 white "├───────────────────────────────────────────────────────────┤"
  put +1,16 "1"  "...."
  put +1    "2"  "...."
  put +1    "G"  "...."
Start:
  Color white on blue
  put %L,1
  Clr1 = "";  Clr2 = "%Clr1"
  Clr3 = "%Clr1";  Clr4 = "%Clr1";  Clr5 = "%Clr1"
  Clr6 = "%Clr1";  Clr7 = "%Clr1";  Clr8 = "%Clr1"
  Clr%N = "blue on white"
  put +2,21 %Clr1 " установить плотную печать "
  put +1    %Clr2 " установить двойную печать "
  put +1    %Clr3 " перезагрузить кириллицу "
  put +1    %Clr4 " установить нормальную печать "
  put +1    %Clr5 " отменить текущую печать "
  put +2    %Clr6 " печать %H в один столбец "
  put +1    %Clr7 " печать %H в два столбца, нормальная "
  put +1    %Clr8 " печать %H в два столбца, плотная "
  C = Getch(/U/N/)

  if "%C" = "ESC"  Cls white on black
    goto Finish;  endif
  if "%C" = "#80"  N = (%N # 8) + 1;            -- клавиша "вниз"
    goto Start;  endif
  if "%C" = "#72"  N = ((%N + 6) # 8) + 1;      -- клавиша "вверх"
    goto Start;  endif
  Коды = "CDLNR12G"
  if "%C" = "ENTER"  C = Sub(%N,1,"%Коды");  endif
  if Not "%StrPos("%C","%Коды")" = 0
    Cls white on black
    if "%C" = "C"
      %comspec /c c:\bat\prninit
    endif
    if "%C" = "D"
      %comspec /c copy c:\bat\dat\double prn
    endif
    if "%C" = "L"
      echo "Данный компьютер в этом не нуждается"
    endif
    if "%C" = "N"
      %comspec /c c:\bat\endcprs
    endif
    if "%C" = "R"
      %COMSPEC /c LbSpool 1,c
    endif
    if "%C" = "1"
      %comspec /c copy %H + c:\bat\dat\2cr prn
    endif
    if "%C" = "2"
      %comspec /c c:\bat\prt %H
    endif
    if "%C" = "G"
      %comspec /c c:\bat\prtc %H
    endif
    goto Finish
  endif
  goto Start

Finish:
  Cursor 6,7
