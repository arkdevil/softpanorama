--
-- Меню дисков -- вызывается из меню NC или из пакета ДОС
-- Copyright (c) 1992 L.G.Bunich, Russia, Moscow
--
  N = 1;  Cls;  Color black on white;  Cursor Off
  Box 4,20 single at 7,29
  put +0,31 red " Выберите диск: "
Dmenu:
  Clr1 = "black on white";  Clr2 = "%Clr1"
  Clr%N = "cyan+ on green"
  put  9,33 %Clr1 " A: "  +0,+3 %Clr2 " B: "
  C = Getch(/U/N/)

  if "%C" = "ESC"  Cls white on black
    goto Finish;  endif
  if "%C" = "#77"  N = 3 - %N;  endif;          -- клавиша "направо"
  if "%C" = "#75"  N = 3 - %N;  endif;          -- клавиша "налево"
  if "%C" = "ENTER"  C = Sub(%N,1,"AB");  endif
  k = StrPos("%C","AB")
  if "%k" = 0  goto Dmenu;  endif
  D = "%C"

  Cls white on black;  Color black on white;  Cursor Off;  N = 1
  Box 09,60 single at 6,12;  L = Cline()
  put %L,24 red " В ы б е р и т е   ф у н к ц и ю: "
  put +2,16 red "D"  "...."
  put +2    red "F"  "...."
  put +2    red "T"  "...."
Fmenu:
  put %L,1
  Clr1 = "black on white";  Clr2 = "%Clr1";  Clr3 = "%Clr1"
  Clr%N = "white+ on blue"
  put +2,21 %Clr1 " копирование дискеты %D%: посекторно"
  put +2    %Clr2 " копирование файлов дискеты %D%: "
  put +2    %Clr3 " тестирование файлов дискеты %D%: "
  C = Getch(/U/N/)

  if "%C" = "ESC"  Cls white on black
    goto Finish;  endif
  if "%C" = "#80"  N = (%N # 3) + 1;            -- клавиша "вниз"
    goto Fmenu;  endif
  if "%C" = "#72"  N = ((%N + 1) # 3) + 1;      -- клавиша "вверх"
    goto Fmenu;  endif
  if "%C" = "ENTER"  C = Sub(%N,1,"DFT");  endif
  W = StrPos("%C","DFT")
  if Not "%W%" = 0
    Cls white on black
    if "%C" = "D"
      %comspec /c  copfd.bat %D
    endif
    if "%C" = "F"
      %comspec /c  c:\bat\dup %D
    endif
    if "%C" = "T"
      %comspec /c  copy /b %D%:*.* NUL
    endif
    goto Finish
  endif
  goto Fmenu
Finish:
  Cursor 6,7
