  Load DOS, FILE
  N=1;  First = 0;  Cls;  Cursor Off; Cset="▐┼▒═"; Nset=0
Start:
  Box 23,80 double white+ on red
  Color black on white
  Nset = (%Nset # 5) + 1;  Filler = Sub(%Nset,1,"%Cset")
  Box 21,76 fill "%Filler" blue
  L = %Cline() + 10
  Box 7,41 at %(%L-9),6 single yellow
  Put +1,8 "Здравствуйте, "  Red  "%FIO%!"
  Put +2   "Система "  Red  "MacroBat"  " приветствует Вас!"
  Put +2   "Выберите один из блоков демонстрации:"

  Box 7,27 at %(%L-9),49 single yellow
  Put +1,51 red "██      █████   █████▌ "
  put +1    red "██     ███  ██  ██  ▐█▌"
  put +1    red "██     ██  ▄▄▄▄ █████▌ "
  put +1    red "██     ███  ██  ██  ▐█▌"
  put +1    red "██████  █████   █████▌ "

  Color black+ on black
  Box 7,20 at %L,08 nofill;	-- тени
  Box 7,20 at %L,33 nofill
  Box 7,20 at %L,58 nofill

Main_Menu:
  Clr1 = "blue on cyan";  Clr2 = "%Clr1";  Clr3 = "%Clr1"
  Clr%N = "white+ on red"
  Color %Clr1
    Box 7,20 at %(%L-1),06
    put +2,11 "Информация"  +1 "о  системе"  +1 " MacroBat "
  Color %Clr2
    Box 7,20 at %(%L-1),31
    put +2,36 " Примеры  "  +1 "запросов к"  +1 "  среде   "
  Color %Clr3
    Box 7,20 at %(%L-1),56
    put +2,61 " Примеры  "  +1 "диалоговых"  +1 " средств  "
  Color black on white
    Put +5,15 " Выберите стрелочными клавишами:  " red "─ ─"
    Put "  нужный блок "  +1,19 " и нажмите "  red "ENTER.  "
    Put red "ESC"  " - конец демонстрации. "
  if %First = 0 then
    Sound K 350,9  D6  350,2  440,4  D4  524,4  D4;   -- Шерлок Холмс
    Sound K 700,2  660,2  584,2  660,2   700,9  D4
    First = 1
  endif
  C = Getch(/N/)
  if "%C" = "ESC" goto Finish; endif
  if "%C" = "#77"  N = (%N # 3) + 1;            -- клавиша "направо"
    goto Main_Menu;  endif
  if "%C" = "#75"  N = ((%N + 1) # 3) + 1;      -- клавиша "налево"
    goto Main_Menu;  endif

  if "%C" = "ENTER" then
    if "%N" = "1"

-- Б Л О К   1

      Color white+ on green;  Box 23,80 single
      Put +2,5 "   Система MacroBat  содержит удобные средства для создания ""интеллекту-"
      Put +1 "альных"" пакетов MS-DOS с самой сложной логикой управления.  Это позволит"
      Put +1 "Вам без труда решать многие практические задачи, например:"
      Color cyan+ on green
      Put +2,10 "- создание установочных и рекламных систем;"
      Put +1 "- разработка красивых диалоговых оболочек чужих систем;"
      Put +1 "- реализация иерархии вторичных меню Norton Commander;"
      Put +1 "- привязка чужих программ к Вашему оборудованию и файловой структу-"
      Put +1 "  ре, к дате выполнения, дню недели, к содержимому Ваших БД и т.д.;"
      Put +1 "- автоматизация труда и сокращение диалога;"
      Put +1 "- просто  уменьшение числа пакетов ДОС,  задание параметров для них"
      Put +1 "  наиболее удобным и компактным способом."
      Color white+ on green
      Put +2,5 "   От многочисленных аналогов MacroBat отличается в первую очередь своим"
      Put +1 yellow "ОТКРЫТЫМ" "  характером.  Это  значит,  что пользователь может подключать к"
      Put +1 "системе  неограниченное количество своих собственных программ (функций),"
      Put +1 "написанных на любом языке."
      Put +1 "   Имеется также собственный мощный макроязык."
      Put +1,"   Эта реклама целиком написана на языке MacroBat!"
      Put +2,27 blink yellow " Нажмите любую клавишу... "
      C = GetCh(/C/)

      Box 23,80 single
      Put +3,14 "   Система MacroBat распространяется  на принципах"
      Put +1 "ShareWare.  Это значит,  что разрешается свободное"
      Put +1 "ее  распространение  и апробация;  если Вы приняли"
      Put +1 "решение  использовать MacroBat,  переведите автору"
      Put +1 "два доллара или 100 рублей."
      Put +2 "   Замечания,  предложения  и  взносы  благодарных"
      Put +1 "пользователей можно присылать по адресу:"
      Color cyan+ on green
      Put +2 "     143013 Московская обл., Одинцовский район,"
      Put +1 "     пос. Немчиновка-1, ул. Агрохимиков, дом 7,"
      Put +1 "     кв. 100  Буничу Л.Г."
      Put +1 "     Тел. для справок:   591-93-09 (домашний)"
      Color white+ on green
      Put +2 "   Система должна распространяться  в неизмененном"
      Put +1 "виде и в полном комплекте.  Коммерческое использо-"
      Put +1 "вание возможно по согласованию с автором."
      Put +3,27  blink yellow " Нажмите любую клавишу... "
      C = GetCh(/C/)
    endif

    if "%N" = "2"

-- Б Л О К   2

      Color black on white;  Box 23,80 single

------- выясним дату и время
      W = 11 * %DOS(GetDate w) - 10
      W = Sub (%W,11,"понедельниквторник    среда      четверг    пятница    суббота    воскресенье")
      День = DOS(GetDate d);  Месяц = DOS(GetDate m);  Год = DOS(GetDate y)
      k = 8 * %Месяц - 7
      Месяц = Sub (%k, 8, "января  февраля марта   апреля  мая     июня    июля    августа сентябряоктября ноября  декабря")
      T = DOS (GetTime h час. m мин. s сек.)
      if %Год > 1980
        Put +2,4 "Сегодня " red "%W, %День %Месяц %Год года; " "время: " red "%T"
      else put +2,4 "Не ленитесь задавать правильную дату!";  endif

------- теперь займемся оборудованием
      Put +1 "Вы работаете на процессоре "  Red "80%DOS( Check CPU )"
      If  "%DOS( Check 87 )" = "Y"
        Put " с математическим сопроцессором"
      else
        Put " без математического сопроцессора"
      endif
      Put +1 "Видеоадаптер: "  red "%DOS( Check Vid )"  ", принтер " red "PRN "
      W = DOS( Ready PRN )
      If "%W" = "Y"  Put "в готовности";  endif
      If "%W" = "N"  Put "не готов";  endif
      If "%W" = "-"  Put "отсутствует";  endif
      If "%DOS( Check Kbd )" = "E"
        put +1 "Клавиатура "  Red "расширенная"  " (AT, 101 клавиша)"
      else
        put +1 "Клавиатура "  Red "стандартная"  " (XT, 83 клавиши)"
      endif
      W=DOS( Check Mouse )
      If "%W" = "N"
        put ", мыши нет"
      else
        put ", есть " red "%W%-кнопочная" " мышь"
      endif
      Dmax = DOS(Check LD)
      put +1 "Доступны дисководы "  Red "A: - %Dmax%:"
      k = 1
Loop:
      D = Sub(%k, 1, "ABCDEFGHIJKLMNOPQRSTUVWXYZ")
      T = DOS( Check LDE=%D );  W=Sub(1,1,"%T")
      if %StrPos("%W","ND") = 0
        i = StrPos("%W", HRFSLV);  i = 12 * %i - 11
        W = Sub(%i%,12,"винчестер   электронный дискетник   ASSIGN/SUBSTсетевой     виртуальный")
        put +1 "     Диск %D%: "  red "%W"
        if "%W%"= "дискетник"
          W = sub(%(9*%sub(3,3,"%T")+1),9,"5"", 360K 5"", 1.2M 3"", 720K 8"" single8"" doublehard diskstreamer 3"", 1.44M"
          put red " - %W";  endif
      endif
      k = %k + 1
      if NOT "%D" == "%Dmax" goto Loop; endif
      put 24,17 blink yellow " Все правильно? Ответьте Y (да) или N (нет) "

Query:
      W = GetCh (/C/U/N/)
      if "%W" = "Y"
        W="Отлично! Но это еще не все..."
      else
        if Not "%W" = "N" GoTo Query; endif
        W="Странно! Тем не менее продолжим..."
      endif

      Color White+ on Blue;  Box 23,80 double
      Put +2,8 yellow "%W"
      Put +3,6 "Текущее оглавление: "  Yellow " %CurDir()"
      W = DOS(GetVers);  T = Sub(2,1,"%W");  W = Sub(3,2,"%W")
      Put +1 "Версия ДОС: "  Yellow " %T%.%W"

      Д = File ( Fsize %COMSPEC )
      Put +1 "Используется процессор команд "
      Put yellow " %COMSPEC"
      if "%Д" = "" then
        Put +1 "  (в настоящий момент недоступный)"
      else
        Put +1 "  Его длина: "  Yellow " %Д "  " байтов "
        T = File(Fdate %COMSPEC)
        День = Sub(7,2,"%T");  Месяц = Sub(5,2,"%T");  Год = Sub(1,4,"%T")
        Put +1 "  Его дата создания (изменения): "
        Put yellow " %День%.%Месяц%.%Год"
      endif

      Put +1 "На диске %CurDir(d) свободно "
      Put Yellow " %DOS( DiskFree ) байтов"
      Put +1 "Свободный участок оперативной памяти: "
      Put yellow " %DOS(MemFree) байтов"
      Put +1 "Свободный участок в области окружения ДОС: "
      Put yellow " %DOS( MemFree ENV ) байтов"
      if "%DOS(Check WINDOWS)" = "Y"
        Put +1 "Работает " yellow "WINDOWS 3.x"
      else
        Put +1 "WINDOWS 3.x " yellow "не активна";  endif
      if "%DOS(Check ANSI)" = "Y"
        Put +1 "Драйвер ANSI.SYS " yellow "активен"
      else
        Put +1 "Драйвер ANSI.SYS " yellow "не активен";  endif
      if "%FILE(NC)" = "Y"
        Put +1 "Norton Commander 3.0 " yellow "активен"
        Put "; текущий файл: " Yellow "%FILE( NC h )"
      else
        Put +1 "Norton Commander 3.0 отсутствует";  endif
      Put 24,27 blink yellow " Нажмите любую клавишу... "
      C = GetCh(/C/)
      if Not "%Д" = "" then
        Cls green on black
        put 2,40 red+ "Сейчас мы вызываем %COMSPEC"
        put +1   red+ " для вывода текущего оглавления..."
        put 1,1
        %COMSPEC /c dir /w /p
        Put 25,27 blink yellow " Нажмите любую клавишу... "
        C = GetCh(/C/); cls
      endif
    endif;  -- конец блока 2

    if "%N" = "3"

-- Б Л О К   3

      N3 = 1
Start3:
      Cls white on black;  Color black on white
      Box 09,50 single;  L3 = Cline()
      put %L3,23 red " В ы б е р и т е   ф у н к ц и ю: "

      do
        put %L3,1;  Cursor OFF
        Clr1 = "black on white";  Clr2 = "%Clr1";  Clr3 = "%Clr1"
        Clr%N3 = "white+ on blue"
        put +2,19 %Clr1 " Просмотр конвертированных ANSI-изображений "
        put +2    %Clr2 " Ввод различных полей "
        put +2    %Clr3 " Вывод под музыку "
        put +2,25 red " ESC - выход к основному меню "
        C = Getch(/U/N/)

        if "%C" = "ESC"    break;  endif
        if "%C" = "ENTER"  break;  endif
        if "%C" = "#80"  N3 = (%N3 # 3) + 1;      endif;  -- клавиша "вниз"
        if "%C" = "#72"  N3 = (%N3 + 1) # 3 + 1;  endif;  -- клавиша "вверх"
      enddo
      if "%C" = "ESC"  Cls white on black;  goto Start;  endif
      if "%C" = "ENTER"
        Cls white on black
        if "%N3" = 1
          MB /m fruits
          Put +2,27  blink yellow " Нажмите любую клавишу... "
          C=GetCh(/U/N/)
        endif
        if "%N3" = 2
          Color cyan+ on blue;  Box 18,60 single;  L1 = Cline
          put +2,17 white+ "Хотите узнать, в какой день недели Вы родились? "
          put +3,20 "Ваш год рождения (4 цифры) .... "
          put +2    "Ваш месяц рождения (1-12) ..... "
          put +2    "Ваш день рождения (1-31) ...... "
          put %(%L1+12),24 white+ "ENTER - принять значение поля"
          put %(%L1+13),24 white+ "ESC - возврат к предыдущему полю"
          Color green+ on black;  Cursor 6,7
          NI = 1;  Y="";  M="";  D=""
          do
            if %NI = 1  Y=GetStr( 4 at %(%L1+05),52 init "%Y");  endif
            if %NI = 2  M=GetStr( 2 at %(%L1+07),52 init "%M" );  endif
            if %NI = 3  D=GetStr( 2 at %(%L1+09),52 init "%D" );  endif

            if "%_EXKEY" = "ENTER"
              NI = %NI + 1;  if %NI > 3 break; endif
            endif
            if "%_EXKEY" = "ESC"
              NI = %NI - 1;  if %NI = 0 break; endif
            endif
          enddo
          if %NI = 0 goto Start3;  endif
          if %M < 3  Y = %Y - 1;  M = %M + 12;  endif
          W = ( %Y + %Y/400 - %Y/100 + %Y/4 + 3*%M - (2*%M+1)/5 + %D ) # 7 + 1
          W = Sub (%(12*%W-11),12," понедельнико вторник    среду       четверг     пятницу     субботу     воскресенье")
          Color cyan+ on blue
          put %(%L1+15),15 blink yellow "Вы родились в%W%, %FIO%!"
          Put +2,27  white+ " Нажмите любую клавишу... "
          Cursor OFF;  C=GetCh(/U/N/)
        endif
        if "%N3" = 3
          Color bright white on blue
          Box 23,78 double
          Box 19,70 double yellow        ;  delay 3
          Box 15,62 double bright red    ;  delay 3
          Box 11,54 double bright magenta;  delay 3
          Box 07,46 double bright green
          L = CLine(); L = %L + 3
          put %L,09 bright green on black " Успехов Вам, "
          Sound K 784 16,  880 08, 880 08, 784 12, 659 04, 523 16
          put bright magenta on black "%FIO%! "
          Sound K 1047 16, 988 08, 880 08, 784 16, 880 16
          put bright blue on black " Используйте "
          Sound K 698 16,  784 08, 784 08, 659 16, 659 08
          put blink red on white " M a c r o B a t! "
          Sound K 740 08, 784 16, 740 16, 659 16, 587 12
          put +4,3
        endif
      endif
      goto Start3

    endif;  -- конец блока 3
    goto Start

  endif;	-- конец исполнения клавиши ENTER
  goto Main_Menu

Finish:
  Box 5,38 at 18,22 bright black on black nofill
  Box 5,38 at 17,20 on black double(yellow on black)
  Put +2,23 yellow on black "Демонстрационный пример закончен"
  Put 24,1
  Cursor 6,7
