


                     M  a  c  r  o  B  a  t
                     ----------------------

              С т а н д а р т н ы е   ф у н к ц и и
              -------------------------------------


                 Редакция.... 1.9  (август 1992)

          (C) 1992 by L.G.Bunich.  ALL RIGHTS RESERVED.



                           Москва 1992



  ------------------- С О Д Е Р Ж А Н И Е -----------------------

  1. Б и б л и о т е к а   ф у н к ц и й.

     1.1.  Встроенные функции.
     1.2.  Комплект DOS.
     1.3.  Комплект FILE.
     1.4.  Стандартные функции для организации циклов.

  2. П р и м е р ы   п а к е т о в.

  3. И н т е р ф е й с   п о л ь з о в а т е л я.

     3.1.  Как подключить функцию пользователя.
     3.2.  Дополнительная таблица.



             1. Б и б л и о т е к а   ф у н к ц и й.
             ---------------------------------------

   Далее принято сокращение: спф - спецификация файла, то есть имя
файла, перед которым могут стоять ДИСК: и/или ПУТЬ\ .

   Некоторые  функции  возвращают составное значение из нескольких
полей. При описании полей для каждого из них указывается номер на-
чального байта  и длина поля (как требуется для функции SUB).  На-
пример, (2,2) означает байты 2-3. Числовые поля в составном значе-
нии всегда прижаты к левому краю.


                    1.1. Встроенные функции.
                    ------------------------

   В  этом  разделе  описаны  стандартные  функции,  встроенные  в
MacroBat; они не нуждаются во внешних исполнителях.

   Функции GETCH и GETSTR  описаны в файле MB.DOC (раздел о диало-
говых средствах).


CCOL(код)  -  получить номер колонки экрана. Нумерация колонок на-
              чинается с единицы.

                CCOL()    - получить номер текущей колонки
                CCOL(MAX) - получить номер последней колонки

CLINE(код)  - получить номер строки экрана.  Нумерация строк начи-
              нается с единицы.

                CLINE()    - получить номер текущей строки
                CLINE(MAX) - получить номер последней строки

COMPNUM(число1,число2) - сравнить два  числовых  значения.  Каждое
              число может принимать значения от нуля до (примерно)
              2 миллиардов. Возвращает одну заглавную букву, обоз-
              начающую результат сравнения:

               L (первое меньше)   G (первое больше)   E (равны)

              Эта функция полезна в BAT-файлах.

CURDIR(код) - выяснить текущий (активный) диск  и/или  текущее ог-
              лавление. Значение будет состоять из заглавных букв.
              Код - одна из букв (заглавная или строчная):

                D - нужен только диск
                P - нужно только оглавление
                A - нужны и диск, и оглавление

              Кроме того, имеются варианты PR  и  AR,  означающие,
              что для корневого оглавления "\" в конце не добавля-
              ется  (чтобы не возникали трудности  с конструкциями
              вида  %D%\*.* ).

              Обращение с пустым аргументом: CURDIR() эквивалентно
              CURDIR(A).
              Примеры:

               ---- (оглавление D:\MB) ----
                CurDir()   возвращает D:\MB
                CurDir(a)  возвращает D:\MB
                CurDir(d)  возвращает D:
                CurDir(p)  возвращает \MB
                CurDir(pr) возвращает \MB

               ---- (корневое оглавление) ----
                CurDir(a)  возвращает D:\
                CurDir(ar) возвращает D:
                CurDir(pr) возвращает пустое значение

EXT(спф)    - возвращает  расширение имени заданного файла,  запи-
              санное заглавными буквами.
              Пример:  Ext(c:\clarion\normy.dat) возвращает DAT.

LOWER("текст") - преобразовать текст в строчный регистр.

NAME(спф)   - возвращает имя заданного файла без расширения, запи-
              санное заглавными буквами.
              Пример:  Name(c:\clarion\norm.dat) возвращает NORM.

STRLEN("текст") - возвращает  длину заданной  строки  (кавычки  не
              считаются).

STRPOS("подстрока","строка")  -  найти,  с какой позиции подстрока
              содержится в указанной строке. Позиции  нумеруются с
              единицы.  Если подстрока не входит в строку, возвра-
              щается нуль.  Длина подстроки и строки - до 250 сим-
              волов каждая. Примеры:

                StrPos("a","alpha")      возвращает  1
                StrPos("b","alpha")      возвращает  0
                StrPos(":\","C:\file1")  возвращает  2

SUB(нач,колич,"текст") - выделить подстроку.  Возвращается <колич>
              символов, начиная с указанного. Если <нач> превышает
              длину текста или колич=0, возвращается  пустая стро-
              ка. Если <колич>  выводит  за  пределы  текста,  оно
              уменьшается до допустимого значения.
                Допускается отрицательное <нач>,  которое означает
              отсчет символов с конца строки (справа налево).
                Примеры.

                Sub(2,4,"квартира")  возвращает:  варт
                Sub(2,9,"квартира")  возвращает:  вартира
                Sub(9,1,"квартира")  возвращает: <пустое значение>
                Sub(-4,2,"квартира") возвращает:  ти

UPPER("текст") - преобразовать текст в заглавный регистр.



                       1.2. Комплект DOS.
                       ------------------

CHECK код       -  выяснить  состав   подключенного  оборудования.
                   У этой функции есть несколько подфункций:

           Check 87   -  узнать,  имеется ли математический сопро-
                         цессор. Возвращается один из кодов:

                              Y (есть)  или  N (нет).

           Check ANSI -  определить, активен ли драйвер  ANSI.SYS.
                         Возвращается один из кодов:

                              Y (активен)  или  N (нет).

                         ВНИМАНИЕ: при этом чистятся четыре симво-
                         ла экрана, начиная с текущей позиции.

           Check CPU  -  определить  тип процессора.  Возвращается
                         один из кодов:

                              86   286   386   486

           Check KBD  -  определить  вид клавиатуры.  Возвращается
                         одна буква:

                              S  (XT-стандартная, 83/84 клавиши)
                              E  (AT-расширенная, 101/102 клавиши)

           Check LD   -  выяснить,  какие дисководы доступны (т.е.
                         подключены,  не  путать  с  готовностью).
                         Возвращается  буквенный  код   последнего
                         доступного дисковода (без двоеточия).

           Check LD=диск - выяснить наличие и тип указанного (бук-
                         вой) дисковода.  Возвращается один из ко-
                         дов:

                              N - устройство не существует
                              D - фиктивный дискетник (dummy); это
                                  означает,  что одному дискетнику
                                  присвоены несколько кодов (обыч-
                                  но A: и B: ).
                              F - реальный дискетник (floppy)
                              H - винчестер (hard disk)
                              L - сетевое устройство (LAN/remote)
                              R - электронный (RAM) диск
                              S - создан ASSIGN или SUBST
                              V - виртуальный дисковод,  созданный
                                  каким-то драйвером.

                           Пример:

                              MB D=DOS(Check LD=C)
                              If Not "%D%"=="H" goto :NoHardDisk

           Check LDE=диск - аналогично, но для дискетников возвра-
                         щается расширенная информация:

                        (1,1) код типа дисковода, как для LD
                        (2,1) код реального дискетника.  Например,
                              если A:  и B:  обозначают один и тот
                              же дисковод,  то LDE=A и LDE=B возв-
                              ращают в этом поле код "A".
                        (3,3) код устройства, например:

                    0 (5-дюймовый, 360K)    3 (8-дюймовый, single)
                    1 (5-дюймовый, 1.2M)    4 (8-дюймовый, double)
                    2 (3-дюймовый, 720K)    7 (3-дюймовый, 1.44M)

           Check Mouse  -  выяснить  наличие  и  тип  манипулятора
                         "мышь". Возвращается один из кодов:

                              N - драйвер мыши не активен
                              2 - обслуживается двухкнопочная мышь
                              3 - обслуживается трехкнопочная мышь

           Check Vid  -  определить  тип  активного видеоадаптера.
                         Возвращается один из кодов:

                             <пусто> - не-графический экран
                               MDA   - экран MDA
                               HGC   - экран Hercules
                               CGA   - экран CGA
                               EGA   - экран EGA
                               MCGA  - экран MCGA
                               VGA   - экран VGA

          Check Windows -  выяснить  наличие  среды  WINDOWS  3.0.
                           Возвращается одна буква:

                            Y (WINDOWS 3.0 запущена)  или N (нет)

DISKFREE диск   -  выяснить  число свободных байтов  на  указанном
                   диске (по умолчанию - на текущем). Диск задает-
                   ся буквой (A, B, C  и  т.д.),  двоеточие  после
                   буквы не обязательно.

GETDATE формат  -  получить текущую дату  и/или день недели  в за-
                   данном формате.  Формат состоит из любых симво-
                   лов (до 250), некоторые из которых имеют специ-
                   альное назначение:

                     d - номер дня.
                     m - номер месяца.
                     y - номер года (четыре цифры).
                     Y - номер года (две цифры).
                     w - номер дня недели  (от 1 до 7, понедельник
                         считается 1-м днем, воскресенье: 7-м).
                     * - указывает, что незначащие нули  в номерах
                         дня и месяца  редактируются  (удаляются).
                         Символ '*', если он нужен,  должен стоять
                         в начале формата. Если он не задан, номе-
                         ра дня и месяца  будут занимать ровно две
                         позиции.
                     ! - следующий символ (d,m,y,Y,w,*,!)  не счи-
                         тать специальным.

                   Любой другой символ  переносится в поле резуль-
                   тата без изменений.
                   Примеры для даты 4 марта 1992 года:

                 MB D=DOS(GetDate d.m.y)   возвращает:  04.03.1992
                 MB D=DOS(GetDate *m/d/Y)  возвращает:  3/4/92
                 MB D=DOS(GetDate На y г.) возвращает: На 1992 г.

                   Еще  пример  (рекомендуется  запускать  его  из
                 AUTOEXEC.BAT):

                   -- если сегодня понедельник, запустим ревизор
                   --   ADINF с личными таблицами и посмотрим,
                   --   какие файлы изменились за неделю
                   If %DOS(GetDate w) = 1
                     c:\private\ADINF C: -p -a -b -d;   EndIf

GETTIME формат  -  получить текущее время в заданном формате. Фор-
                   мат состоит из любых символов (до  80), некото-
                   рые из которых имеют специальное назначение:

                     h - часы (0-23).
                     m - минуты (0-59).
                     s - секунды (0-59).
                     t - сотые доли секунды (0-99).
                     * - указывает, что незначащие  нули  во  всех
                         числовых полях редактируются (удаляются).
                         Символ '*', если он нужен,  должен стоять
                         в начале формата. Если он не  задан, поля
                         будут занимать ровно две позиции каждое.
                     ! - следующий символ (h,m,s,t,*,!) не считать
                         специальным.

                   Любой другой символ  переносится в поле резуль-
                   тата без изменений.
                   Примеры для момента 9 часов 19 минут 5 секунд:

                MB D=DOS(GetTime h:m)       возвращает:  09:19
                MB D=DOS(GetTime *hч mм sс) возвращает:  9ч 19м 5с

GETVERS         -  выяснить версию ДОС. Возвращаются четыре цифры:
                   VVMM,  например:

                     0330 (MS-DOS 3.30),  0500 (MS-DOS 5.00)

MEMFREE код     -  если <код> не задан,  то эта функция определяет
                   размер  свободного  участка оперативной памяти.
                   При этом:
                     -  если функция вызвана  в BAT-файле ДОС,  то
                   память, занятая самим MacroBat, не учитывается,
                   т.е. считается свободной;
                     -  если функция вызвана в процедуре MacroBat,
                   возвращается истинная величина свободной памяти
                   (память  для  MacroBat  и закрепленных  по LOAD
                   программ считается занятой).
                     Если задан код ENV, возвращаемое значение со-
                   общает, сколько байтов осталось в главной обла-
                   сти окружения текущего командного процессора.

READY принтер   -  проверить наличие и готовность принтера.  Прин-
                   тер обозначается кодом PRN или Pномер (P1 - это
                   PRN, он же LPT1, P2 - это LPT2 и т.д.).
                      Возвращается один символ:

                        Y   (устройство готово)
                        N   (устройство не готово)
                        -   (устройство не подключено)

                      Пример работы с этой функцией:

                        If "%DOS(Ready PRN)" = "Y"
                          %ComSpec /c c:\bat\PRNinit.bat
                        EndIf

                      В примере,  если готов принтер,  выполняется
                   инициализирующий  BAT-файл (например,  загрузка
                   шрифта и установка плотности печати).



                       1.3. Комплект FILE.
                       -------------------

CLOSE номер_файла  - закрыть файл, открытый функцией OPEN. Возвра-
                   щаемое значение пусто.

DELETE спф       - удалить файл. Возвращает один из кодов:

                       <пусто> - файл успешно удален
                          N    - файл не найден

ERASE спф        - то же, что и DELETE.

FATTR спф        - получить  атрибуты файла.  Возвращается восьми-
                   символьная переменная следующей структуры:

                        (1) R  (read only) - только для чтения
                        (2) H  (hidden) - скрытый файл
                        (3) S  (system) - системный файл
                        (4) V  (volume label) - метка тома
                        (5) D  (directory) - подоглавление
                        (6) A  (archive) - архивная пометка
                        (7-8) - резерв

                     Если  файл  не имеет указанного атрибута,  на
                   соответствующем месте значения будет пробел.

FDATE спф        - выяснить дату изменения заданного файла (в бай-
                   тах). Возвращаемое значение состоит  из  восьми
                   цифр: ГГГГММДД (год-месяц-день). Если  файл  не
                   найден, возвращается пустое значение.

FINDPFX префикс  - найти файл, имя которого начинается с заданного
                   текста (префикса). Префикс может  включать диск
                   и/или оглавление; по умолчанию поиск происходит
                   в текущем оглавлении  (плюс оглавления, опреде-
                   ленные командой APPEND).  Допускаются трафареты
                   имен (с символами '*' и '?').  Можно  задать  и
                   точное  имя файла;  если у него нет расширения,
                   надо в конце имени указать точку.
                     Возвращается полная  спецификация  найденного
                   файла (заглавными буквами).
                     Если таких файлов  нет,  возвращается  пустое
                   значение;  если  их несколько,  берется  первый
                   найденный.
                     Примеры:

                       MB D=FILE(FindPfx Uni)
                       MB D=FILE(FindPfx *.ZIP)
                       MB D=FILE(FindPfx MYFUN.ASM)
                       MB D=FILE(FindPfx arch.???)

FNAME /формат/,спф - редактирование спецификации файла по заданно-
                   му формату.
                     спф - полная или неполная спецификация файла;
                   MacroBat добавляет  недостающие  поля  и  затем
                   возвращает значение согласно заданному формату.
                     Формат состоит из произвольных  символов  (до
                   60), некоторые  из  которых  имеют  специальное
                   назначение:

                    d - код устройства (например, C:)
                    p - путь к файлу; он начинается с "\",  а если
                        после кода "p" идет код  "n",  между  ними
                        вставляется "\".
                    f - имя файла с расширением
                    n - имя файла без расширения
                    e - расширение имени (с точкой впереди)
                    ! - следующий символ не считать специальным

                     Любой другой символ  переносится  в  поле ре-
                   зультата без изменений.  Порядок  и  количество
                   форматных кодов не играют роли.
                     Все поля спецификации переводятся в заглавный
                   регистр.
                     Если формат не задан,  подразумевается формат
                   dpf, т.е. полная спецификация.
                     Примеры (для текущего оглавления C:\SYS\MB):

            Fname(dos.com)          возвращает:  C:\SYS\MB\DOS.COM
            Fname(/dp\*.e/,dos.com) возвращает:  C:\SYS\MB\*.COM
            Fname(/pn/,c:dos.com)   возвращает:  \SYS\MB\DOS

FSIZE спф        - выяснить длину заданного файла (в байтах). Если
                   файл не найден, возвращается пустое значение.

NC код           - обслуживание среды Norton Commander 3.0.
                   ВНИМАНИЕ:  Commander  должен быть запущен через
                     NC, а не NCMAIN!
                   У этой функции есть несколько подфункций:

         NC           - выяснить, запущен ли Norton Commander.
                        Возвращает значение Y (да) или N (нет).
         NC H         - выяснить имя текущего файла (highlighted).
         NC H2        - то же для неактивной панели.
         NC H:формат  - получить характеристики текущего файла со-
                        гласно  формату.  Формат - до трех (подряд
                        записанных) кодов:

                           D - включить в результат текущий диск
                           P - включить в результат текущий путь
                           F - включить в результат имя файла

         NC H2:формат - то же для неактивной панели.

                      Примеры для файла D:\MACROBAT\ARCH.MB:

           NC h:d   возвращает:  D:
           NC h:dp  возвращает:  D:\MACROBAT
           NC h:pf  возвращает:  \MACROBAT\arch.mb
           NC h:dpf возвращает:  D:\MACROBAT\arch.mb

                            (NC формирует имена файлов из строчных
                             букв, а оглавлений - из заглавных)

         NC M         - цикл по отмеченным файлам, описана ниже, в
                        параграфе об управляющих функциях циклов.

OPEN O=спф       - открыть выходной файл.  Если такой файл уже су-
                   ществует, он уничтожается. Возвращается ДОС-но-
                   мер  файла  (file  handle),  который  позже ис-
                   пользуется функцией PUTLINE.
                     ВНИМАНИЕ:  прежде  чем открывать файлы,  надо
                   закрепить комплект FILE в памяти  (см. пример в
                   описании PUTLINE)!

PUTLINE номер_файла,"текст"  -  вывести  строку  в текстовый  файл
                   (ранее  открытый  функцией OPEN).  Возвращается
                   пустое  значение.  В  случае  ошибки (например,
                   файл не открыт) выводится диагностика.
                     Не забудьте  по окончании вывода закрыть файл
                   (функция CLOSE).
                     Пример:  выведем  в файл  TEMP.DOC все строки
                   данного документа, содержащие контекст "это".

                        Load FILE
                        h=FILE( Open o=temp.doc )
                        do s=FILE( GetLine MB.DOC )
                          if %StrPos("это","%s%") > 0
                            FILE PutLine %h,"%s"
                          endif
                        enddo



        1.4.  Стандартные функции для организации циклов.
        -------------------------------------------------

FNEXT(трафарет)  - найти  очередное  имя  файла.  В ходе цикла эта
                функция  возвращает  по  очереди все имена файлов,
                соответствующие указанному трафарету имен. Пример:

                        do Имя=Fnext(*.txt)
                          .......
                        enddo

                   В  трафарете   можно  использовать  метасимволы
                (wildcards) "*" и "?", как и в MS-DOS.  Если надо,
                укажите путь, например:  W=FNEXT(C:\DB\*.DBF) .
                   Трафарет *.* (все файлы) может быть опущен,  он
                подразумевается по умолчанию.
                   Кроме  имени  файла,  функция FNEXT формирует в
                системной  переменной  _FATTR коды атрибутов этого
                файла. _FATTR - восьмисимвольная переменная следу-
                ющей структуры:

                        (1) R  (read only) - только для чтения
                        (2) H  (hidden) - скрытый файл
                        (3) S  (system) - системный файл
                        (4) V  (volume label) - метка тома
                        (5) D  (directory) - подоглавление
                        (6) A  (archive) - архивная пометка
                        (7-8) - резерв

                   Если файл не имеет указанного атрибута,  на со-
                ответствующем месте в _FATTR будет пробел.

                   Пример - цикл отбора подоглавлений текущего ог-
                лавления:

                        do Имя = Fnext(c:\*.*)
                          if not "%sub(5,1,"%_Fattr")" = "D"
                            cycle; endif
                          echo "%Имя"
                        enddo

                   Замечание:  внутри  FNEXT-циклов не допускаются
                другие FNEXT-циклы (область DTA занята!).

FILE( GETLINE спф ) - читать  очередную  строку  текстового файла.
                Эта функция позволяет перебрать в цикле все строки
                указанного файла.  По концу файла он автоматически
                закрывается;  если  же  цикл  прекращен оператором
                BREAK,  то  для закрытия файла нужно обращение без
                спф:

                     имя = FILE( GETLINE )

                или просто:

                     FILE GETLINE

                   Замечания.
                   1. Внутри GETLINE-циклов  не допускаются другие
                GETLINE-циклы.
                   2. Парная  к  GETLINE  функция   вывода  текста
                (PUTLINE) описана в параграфе "Комплект FILE".

                   Пример:

                        Load FILE
                        Do S = FILE(GetLine MB.ASM)
                          If %StrPos("equ","%S") > 0
                            Echo "%S";  endif
                        Enddo

                   В примере  отбираются  все строки файла MB.ASM,
                содержащие контекст "equ".

FILE( NC M )  - получить имя файла,  отмеченного на текущей панели
                Norton  Commander 3.0.  Здесь организуется перебор
                всех отмеченных файлов.
                  Пример: создадим файл со списком отмеченных фай-
                лов и используем его для запуска архиватора.

                     Load FILE
                     h = FILE( Open O=temp.$$$ )
                     do S = FILE( NC m )
                       FILE Putline %h,"%s"
                     enddo
                     FILE Close %h
                     arj a MYARCH !temp.$$$
                     FILE Delete temp.$$$



                2. П р и м е р ы   п а к е т о в.
                ---------------------------------


   (А) Запоминание и восстановление текущего оглавления ДОС.

       Пакет PUSHD.BAT                   Пакет POPD.BAT
       ---------------                   --------------
        MB  D=CurDir(d)                   %D%
        MB  P=CurDir(p)                   CD  %P%
                                          Set D=
                                          Set P=

   Первая строка  в PUSHD.BAT  присваивает  переменной окружения D
значение встроенной функции CurDir(d), т.е. имя  текущего дисково-
да.  Далее функция CurDir(p) присваивает переменной P имя текущего
оглавления. После этого можно перейти в другое оглавление, вызвать
нужные программы, а затем вернуться в исходную точку с помощью па-
кета POPD.BAT.
   Пример пакета PRINCE.BAT:

          Call PushD
          CD \games\prince
          Prince
          PopD


   (Б) Выдача поздравления в день рождения
       (фрагмент пакета AUTOEXEC.BAT).

        MB D=DOS(GetDate dm)
        if NOT "%D%"=="0610" goto :Next
          echo ***************************************************
          echo *  H a p p y   B i r t h d a y   T o   Y o u !!!  *
          echo *  H a p p y   B i r t h d a y   T o   Y o u !!!  *
          echo ***************************************************
        :Next
           .........................


   (В) Проверка, соответствует ли индекс  базе  данных  (Clipper).
       Если  дата  изменения базы данных не такая,  как у индекса,
       значит, индекс устарел, и происходит его обновление.

            MB DF=FILE(FDate KADRY.DBF)
            MB DI=FILE(FDate KADRY.NTX)
            if NOT %DI%==%DF%  INDEX KADRY
            ................


   (Г) Вывод списка имеющихся дисков и их типов.

            @echo off
              Set k=1
            :Loop
              MB D=Sub(%k%,1,"ABCDEFGHIJKLMNOPQRSTUVWXYZ")
              MB W=DOS(Check LD=%D%)
              if %W%==N goto :Next
              if %W%==D echo   Диск %D%: фиктивный
              if %W%==F echo   Диск %D%: дискетник
              if %W%==H echo   Диск %D%: винчестер
              if %W%==L echo   Диск %D%: сетевой
              if %W%==R echo   Диск %D%: электронный
              if %W%==S echo   Диск %D%: создан ASSIGN или SUBST
              if %W%==V echo   Диск %D%: создан драйвером
            :Next
              MB k=%k%+1
              if Not %k%==26 goto :Loop
              Set W=
              Set k=
              Set D=

   (Д) Процедура вывода меню имеющихся дисков.  Обратите внимание,
       как формируются плитка переменной ширины и ее содержимое.

           N = 1;  Cls;  Color black on white;  Cursor Off
           Dmax = DOS(Check LD);           -- последний диск
           kmax = StrPos("%Dmax","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
           Box 3,%(4 * (%kmax + 1)) single
           col = CCol();  put +1,%col;     -- входим внутрь плитки
         Start:
           k = 1;  put +0,%(%col+1)
         Loop:                             -- цикл по дискам
           D = Sub(%k, 1, "ABCDEFGHIJKLMNOPQRSTUVWXYZ")
           if NOT "%DOS( Check LD=%D )" == "N"
             Clr = ""
             if "%k" = "%N"  Clr = "cyan+ on green";  endif
             put +0,+1 %Clr " %D%:"
           endif
           k = %k + 1
           if NOT "%D" == "%Dmax" goto Loop; endif
                                           -- конец цикла
           C = Getch(/U/N/);               -- меню выведено

           if "%C" = "ESC"  Cls white on black
             goto Finish;  endif
           if "%C" = "#77";                -- клавиша "направо"
             N = (%N # %kmax) + 1
             goto Start;  endif
           if "%C" = "#75";                -- клавиша "налево"
             N = ((%N + %kmax - 2) # %kmax) + 1
             goto Start;  endif
           if "%C" = "ENTER"
             C = Sub(%N,1,"ABCDEFGHIJKLMNOPQRSTUVWXYZ");  endif
           if %StrPos("%C","ABCDEFGHIJKLMNOPQRSTUVWXYZ") = 0
             goto Start; endif

         -- здесь должен стоять блок исполнения

         Finish:
           Cursor 6,7

   (Е) Простой целочисленный калькулятор
       (см. CALC.MB в поставке).

         cls
         Color white on blue double
         Box 7,70 at 9, 8 double
         put 9,35 " Калькулятор "
         put 11,12 "Формула:" 13,12 "Результат: "
         Box 1,50 at 11,23 on black
         F = GetStr()
         R=%(%F)
         Put 13,23 white on black "%R"


   См. также текст DEMO.MB, это тоже богатый пример.



         3. И н т е р ф е й с   п о л ь з о в а т е л я.
         -----------------------------------------------

            3.1. Как подключить функцию пользователя.
            -----------------------------------------

   Как  уже  говорилось,  Вы  можете  подключать к MacroBat произ-
вольное число собственных программ, которые вызываются так же, как
и стандартные. Программа пользователя может быть написана на любом
языке  и должна быть скомпонована в виде COM- или EXE-файла.  Файл
программы может быть расположен в одном из оглавлений, перечислен-
ных в параграфе "Запуск внешних программ".

   Функция получает текстовую строку в качестве аргумента  и может
вернуть текстовое значение, которое присваивается переменной окру-
жения.

   Несколько небольших функций  рекомендуется для экономии объеди-
нять в комплекты (см. выше).

   Для связи с вызываемой  программой  MacroBat  предлагает  новую
функцию 11-го прерывания. На ассемблере это выглядит так.

        MOV     AX,"F0"
        MOV     DI,"MB"
        INT     11h
        CMP     AX,0            ; вызвана через MacroBat?
        JNE     Finish          ;   (нет - конец работы)


   Результаты обращения:        ES:DI - адрес блока связи (MBCB)
                                AX    - нуль

   Блок MBCB устроен следующим образом.

    смещение    тип     назначение
    --------    ---     ----------
      (-2)     слово    адрес дополнительной таблицы
      (+0)     байт     флажки
      (+1)     байт     код завершения
      (+2)     слово    длина текста аргумента
      (+4)     байты    текст аргумента (до 250 байтов), затем 00h

   При вызове функции пользователя аргумент находится в поле текс-
та MBCB (начиная со смещения +4). В конце его, как принято в языке
Си,  стоит нулевой байт (так что длина текста вам может и не пона-
добиться).

   Перед окончанием работы  функция должна занести  код завершения
(нуль, если все в порядке), длину возвращаемого  значения  и  само
значение (в поле текста, нулевой байт в конце не обязателен). Пус-
тое значение имеет длину, равную нулю.

   В случае ошибки функция должна определить  какое-нибудь возвра-
щаемое значение, а также занести ненулевой код завершения, который
вызывает немедленное прекращение работы MacroBat.

   Пример заголовка программы на языке Турбо-Си:

void main {

  struct {                      /* блок MBCB */
        char flags,rc;
        int plen;
        char txt[];
  } far *parg;
     ............................
  asm {
        mov  ax,"F0"
        mov  di,"MB"
        int  11h
        cmp  ax,0
        jne  Finish
        mov  word ptr parg,di
        mov  word ptr parg+2,es
      }
     ............................

   Можно, конечно, обратиться и к библиотечной функции INT86.

   При  составлении  собственной  функции для организации цикла Вы
должны следовать, помимо описанных, еще следующим правилам.

   (A) Старший  бит  байта  флажков в MBCB  при первом обращении к
функции равен 1.  Проверьте его в программе, если нужны какие-либо
действия по инициализации цикла (скажем, открытие базы данных).

   (B) Когда  цикл надо завершить,  функция  должна вернуть специ-
альное значение: 0606h (пики).

   (C) Если по окончании цикла (нормальном или по BREAK) необходи-
мы какие-либо завершающие действия  (например,  закрытие базы дан-
ных), предусмотрите для этого особый формат обращения.

   ВНИМАНИЕ: в настоящей редакции все закрепляемые программы долж-
ны удовлетворять некоторым дополнительным ограничениям;  в частно-
сти, запрещаются операции динамического запроса памяти.

   Замечания.
   ----------

   1. Функция пользователя должна  завершать работу  обычным обра-
зом: через INT 20h или INT 21h (AH=4Ch). Для закрепляемых в памяти
программ допустим только первый способ (INT 20h).

   2. При отладке своих функций (с помощью TD, CODEVIEW и т.д.) Вы
можете встретиться с трудностями,  так как функции загружаются ди-
намически, и в них нельзя сразу расставить точки отладки. Рекомен-
дуется простой способ: включить в исходный текст функции (в нужных
местах) команды отладчика (INT 3),  которые не мешают выполнению и
вызывают в присутствии отладчика приостановку.



                  3.2. Дополнительная таблица.
                  ----------------------------

   Дополнительная таблица содержит полезные сведения  и  может об-
легчить Вам программирование своих функций.  Она базируется тем же
регистром, что и MBCB (т.е. ES) и устроена следующим образом.

    смещение    тип       назначение
    --------    ---       ----------
      (+0)     слово     сегментный адрес ПСП MacroBat
      (+2)     байт      параметр N для ResEdit (см. ниже)
      (+3)     байт      резерв - 1 байт
      (+4)     слово     сегментный адрес конца занятой памяти
      (+6)     байт      GSW - общие флажки
                           80h - процедура (1) или BAT-файл (0)
      (+7)     байты     резерв - 3 байта
     (+10)     слово     адрес таблицы областей окружения
     (+12) двойное слово адрес подпрограммы редактирования ResEdit

   Таблица областей  (TEA)  состоит пока из двух строк (по 3 слова
каждая) следующего формата:

    (1) адрес главной области (двойное слово) и ее длина (слово)
    (2) адрес локальной области (двойное слово) и ее длина (слово)

   Подпрограмма ResEdit поможет Вам  отредактировать  числовую ин-
формацию (32-битовые числа). Перед обращением к ней надо заслать:

        - редактируемое двоичное число в (DX,AX);
        - требуемое число цифр в поле N (смещение +2 в таблице);
          нуль означает: занести сколько получится, подавив нули;
        - начальный адрес результата в ES:DI (обычно MBCB+4).

   Результат: занесено отредактированное  десятичное  значение  по
указанному адресу. Кроме того, поле N содержит  фактическое  число
занесенных цифр,  а ES:DI указывает на байт,  следующий  за концом
числа.

   Схема функции пользователя с вызовом ResEdit:

.model tiny
.code
        org     100h

Exam    proc    far
        jmp     Start

; здесь можно поместить поля данных

Start:
        mov     ax,"F0"
        mov     di,"MB"
        int     11h
        cmp     ax,0                    ; вызов через MACROBAT?
        je      RightCall               ;  (да)
        jmp     Finish                  ;  (нет)
RightCall:
        mov     bx,word ptr es:[di-2]   ; адрес ComTab
        mov     byte ptr es:[bx+2],0    ; N=0

; здесь надо занести редактируемое 32-битовое значение в DX и AX
; как всегда, в DX - старшие цифры значения, в AX - младшие

        push    di
        add     di,4                    ; адрес результата
        call    dword ptr es:[bx+12]    ; вызов ResEdit
        pop     di
        mov     al,byte ptr es:[bx+2]   ; заносим длину из N
        mov     byte ptr es:[di+2],al
Finish:
        mov     ax,4C00h                ; выход
        int     21h

Exam    endp
        end     Exam

