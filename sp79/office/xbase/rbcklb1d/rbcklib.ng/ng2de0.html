<HTML>
<HEAD><TITLE>RBckLib v1.0d Clipper Backup Library - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ngbb1.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng20c.html">[^^Up^^]</A>
<A HREF="ng54bb.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>
 <B>R_Restore()</B>
 Restores files backed up by the R_BackUp()-function
───────────────────────────────────────────────────────────────────────────────

 <B>Syntax</B>
        nRetCode := R_Restore ( [ cSrcDrv [, aMask [, cDestSpec
                        [, cFileName
                        [, aBar
                        [, lEscape [, lSilent [, bRBckBlk
                        [, aMsg
                        [, bConfirm
                        ] ] ] ] ] ] ] ] ] )

 <B>Arguments</B>
        cSrcDrv         Source drive for restore : 'A:', 'B:', ....

                        Default : <B>'A:'</B>
                        ─────────────────────────────────────────────────────
        aMask           ARRAY with file masks of files to restore
                                p.e. { '*.FRM' } or { '*.DBF', '*.NTX' }

                        A mask may contain the DOS wildcards '*' and '?';
                        DRIVES and/or DIRECTORIES are NOT allowed ...

                        Default : <B>{ '*.*' } (= ALL files in the backup-set)</B>
                        ─────────────────────────────────────────────────────
        cDestSpec       Destination specification (DRIVE and/or DIRECTORY)
                                p.e. 'C:\TEMPDIR'

                        Notes   : - The specification may NOT contain
                                    FILE NAMES (nor extensions)

                                  - If NO (or an EMPTY) DESTINATION
                                    SPECIFICATION is passed, the files will
                                    be restored to the drive and directory
                                    from which they were backed up (the path
                                    names are stored in the backup-set)

                                  - If no DRIVE is passed, the default drive
                                    will be assumed.

                                  - If no DIRECTORY is passed, the default
                                    directory will be assumed.

                                  - If the TARGET DIRECTORY doesn't exist
                                    it will be CREATED !

                        Default : <B>The DRIVE and DIRECTORY from which</B>
                                  <B>the files originally were backed up</B>
                        ─────────────────────────────────────────────────────
        cFileName       File name of the backup file to be used for restore
                        (w/o extension)

                        Notes   : - Maximal string length is 8 characters.
                                  - The extension of the backup file name is
                                    a sequence number ( '.000', '.001', ...)
                                    that is generated by RBackUp().

                        Default : <B>'@RBackUp'</B>
                        ─────────────────────────────────────────────────────
        aBar[5]         Array containing the PROGRESS BAR definition
                        (The BAR_* constants are pre-defined in <B>RBckLib.ch</B>)

                        aBar[1]         LENGTH of the progress bar
                        aBar[BAR_LEN]   Range   : 0 to MaxCol()
                                        Default : <B>0 = NO PROGRESS BAR</B>

                        aBar[2]         Screen ROW for the progress bar
                        aBar[BAR_ROW]   Range   : 0 to MaxRow()
                                        Default : <B>Centered</B>

                        aBar[3]         Screen COLUMN for the progress bar
                        aBar[BAR_COL]   Range   : 0 to MaxCol()-aBar[1]
                                        Default : <B>Centered</B>

                        aBar[4]         CHARACTER used for displaying the
                        aBar[BAR_CHR]   progress bar
                                        Default : <B>█ = Chr (219)</B>

                        aBar[5]         COLOR of the progress bar
                        aBar[BAR_CLR]   (Clipper color string)
                                        Default : <B>'GR+/G' = Yellow on Green</B>
                        ─────────────────────────────────────────────────────
        lEscape         .F.     : R_Restore() can't be interrupted
                        .T.     : R_Restore() may be interrupted by pressing
                                  the &lt;Esc&gt;-key

                        Default : <B>.F. = &lt;Esc&gt;-key DISABLED</B>
                        ─────────────────────────────────────────────────────
        lSilent         .T.     : No alert sounds
                        .F.     : Give a beep when displaying 'INSERT DISK'
                                  message

                        Default : <B>.F. = Give a beep</B>
                        ─────────────────────────────────────────────────────
        bRBckBlk        CodeBlock that is evaluated once for each file, just
                        before it is extracted from the backup-set.

                        Notes   : - The CURRENT FILE NAME is passed to
                                    bRBckBlk (Format : d:\path\filename.ext)

                                  - bRBckBlk, when used, should return one of
                                    the following values :

                                    <B>0 = BU_ABORT</B>
                                        Restore process will be ABORTED.
                                        In this case, R_Restore() returns
                                        the <B>BU_USER_ABORT</B> return code

                                    <B>1 = BU_CONT</B>
                                        Restore process will CONTINUE.

                                    (These constants are pre-defined in
                                     <B>RBckLib.CH</B>)

                        Default : <B>{ || BU_CONT }</B>
                        ─────────────────────────────────────────────────────
        aMsg[4]         Array containing definition of the 'INSERT NEXT DISK'
                        message.
                        (The MSG_* constants are pre-defined in <B>RBckLib.ch</B>)

                        aMsg[1]         ROW for displaying the 'INSERT NEXT
                        aMsg[MSG_ROW]   DISK'-message
                                        Range   : 0 to MaxRow()
                                        Default : <B>Centered</B>

                        aMsg[2]         Screen COLUMN for displaying the
                        aMsg[MSG_COL]   'INSERT NEXT DISK'-message
                                        Range   : 0 to MaxCol()-Len(aMsg[3])
                                        Default : <B>Centered</B>

                        aMsg[3]         'INSERT NEXT DISK'-message to display
                        aMsg[MSG_TXT]   Message that will be displayed when
                                        the user has to insert the next
                                        backup disk.
                                        The DISK NUMBER is generated by
                                        R_Restore().
                                        Insert '<B>XXX</B>' in the aMsg[3]-string
                                        on the position where the <B>DISK NUMBER</B>
                                        should be substituted.
                                        P.e. :
                                        'Please insert disk #XXX into A:'
                                        On runtime the XXX will be replaced
                                        with the current disk number.
                                        Default :
                                        If lEscape = .T.
               <B>'Insert backup disk # XXX into drive d:  &lt;─┘=Okay, Esc=Cancel'</B>
                                        If lEscape = .F.
               <B>'Insert backup disk # XXX into drive d:  Press &lt;─┘ when ready'</B>
                                        (For d:, the actual backup drive
                                        will be substituted)

                        aMsg[4]         COLOR used to display the 'INSERT
                        aMsg[MSG_CLR]   NEXT DISK'-message
                                        (Clipper color string)
                                        Default : <B>'W+*/B' = Blinking Bright</B>
                                                            <B>White on Blue</B>
                        ─────────────────────────────────────────────────────
        bConfirm        CodeBlock that is evaluated if the file to be
                        extracted already exists on the restore location
                        (cDestSpec or original drive\path)

                        Notes   : - The file name of the file that might be
                                    overwritten is passed to bConfirm.

                                  - bConfirm, when used, should return one of
                                    the following values :
                                    <B>0 = BU_ABORT</B>
                                          R_Restore() will be aborted
                                          In this case, R_Restore() returns
                                          the <B>BU_USER_ABORT</B> return code

                                    <B>2 = BU_SKIPFILE</B>
                                          File will NOT be restored.

                                    <B>3 = BU_OVERWRITE</B>
                                          Existing file will be OVERWRITTEN.

                                    (These constants are pre-defined in
                                     <B>RBckLib.CH</B>)

                        Default : <B>{ || BU_OVERWRITE }</B>
                        ─────────────────────────────────────────────────────

 <B>Returns</B>
        nRetCode        Return code

                        Possible return codes :

                         0 = BU_OKAY           No errors detected
                         1 = BU_INVALID_PARM   Invalid parameter(s) passed
                         2 = BU_OPEN_INPUT     Error opening  input  file
                         3 = BU_WRONG_VERSION  Wrong version of RBckLib
                         4 = BU_CREATE_OUTPUT  Error creating output file
                         5 = BU_READ_INPUT     Error reading  input  file
                         6 = BU_WRITE_OUTPUT   Error writing  output file
                         8 = BU_USER_ABORT     Backup aborted by user
                         9 = BU_INVALID_DRIVE  Invalid backup drive/disk
                        10 = BU_INVALID_DIR    Invalid restore directory

                        See also the <B>ErrorCodes</B>-section in this guide.

 <B>Description</B>
        Restores one or more files from a backup-set, created by the
        R_BackUp()-function.

        Notes :
        - Make sure that the files to be restored are closed :
          dbCloseAll (); dbCommitAll ()

        - If you included a copy of the running .EXE file in the backup-set,
          it is not possible to restore the file to it's original location
          (the running copy is opened ...).
          The only way to extract the file is to another location.
          (Use the cDestSpec parameter)

 <B>Example</B>
        *-- Header file of RBckLib
        #include "RBckLib.CH"

        LOCAL   nRetCode                        &amp;&amp; Return code R_Restore()
        LOCAL   aBckError := BU_ERRMSG          &amp;&amp; Array with error msgs

        *------------------------------------------------------------------
        * Source drive          : 'A:'
        * Files to restore      : '*.DBF' and '*.NTX'
        * Destination           : Extract to original location (default)
        * Backup file name      : '@RBackUp' (default)
        * Progress bar          : Length    = 50
        *                         Row       = 22
        *                         Column    = Centered (default)
        *                         Character = Chr (177) (▒)
        *                         Color     = 'GR+/B'
        * Interruptable         : Not interruptable (default)
        * CodeBlock for name    : { |fname| ShowName ( fname ) }
        * Insert disk message   : Row       = 24
        *                         Column    = Centered (default)
        *                         Msg Text  = New message
        *                         Msg Color = 'GR+/B'
        * Confirmation CodeBlock: { |fname| Confirm ( fname ) }
        *------------------------------------------------------------------
        nRetCode := <B>R_Restore</B> ( 'A:', { '*.DBF', '*.NTX' }, nil,  nil, ;
                        { 50, 22, nil, Chr (177), 'GR+/B' }, ;
                        nil, { |fname| ShowName ( fname ) }, ;
                        { 24, nil, 'Insert disk XXX into drive A:','GR+/B' }, ;
                        { |fname| Confirm ( fname ) } )

        IF nRetCode != BU_OKAY
                *-- Oops, error while restoring ...

                *-- Display the error message text
                ALERT ( 'Restore error : ' + aBckError [ nRetCode ] )
        ENDIF

        *-- Continuation of the program
        [...]

        QUIT

        *------------------------------------------------------------------
        * Display the name of the file being backed up
        *------------------------------------------------------------------
        STATIC FUNCTION ShowName ( cFileName )

        DevPos ( 14, 0 )
        DevOut ( 'Processing : ' + PadR ( cFileName, 40 ) )

        RETURN  BU_CONT                 &amp;&amp; Continue backup

        *------------------------------------------------------------------
        * User confirmation for overwriting an existing file
        *------------------------------------------------------------------
        STATIC FUNCTION Confirm ( cFileName )

        LOCAL   nChoice := Alert ( 'File ' + cFileName + ' exists !', ;
                                    { 'Skip file', 'Overwrite', 'Abort' } )
        DO CASE
        CASE nChoice = 1
                *-- Don't overwrite file !
                RETURN BU_SKIPFILE
        CASE nChoice = 2
                *-- Overwrite file !
                RETURN BU_OVERWRITE
        ENDCASE

        RETURN BU_ABORT         &amp;&amp; nChoice = 3 : ABORT R_Restore()

        More examples can be found in the demo program <B>RBckDemo.PRG</B>.

 <B>Caution</B>

        ┌────────────────────────────────────────────────────────────────╖
        │                         -+- CAUTION -+-                        ║
        │                                                                ║
        │     IN THE UN-REGISTERED  VERSION OF RBCKLIB THE FOLLOWING     ║
        │     REGISTRATION MESSAGE WILL BE DISPLAYED AT RANDOM TIMES     ║
        │          (WHILE EXECUTING THE R_Restore() FUNCTION) :          ║
        │                                                                ║
        │ <B>UN-Registered version of RBckLib -+- Please wait for 9 seconds</B> ║
        │                                                                ║
        │       AFTER A 9 SECONDS DELAY THE PROGRAM WILL CONTINUE.       ║
        ╘════════════════════════════════════════════════════════════════╝

 <B>о</B>
</PRE>
<HR>
<B>See Also:</B>
<A HREF="ngbb1.html">R_BackUp()</A>
<A HREF="ng54bb.html">R_BckList()</A>
<A HREF="nge7bd.html">ErrorCodes</A>
<A HREF="ngfe70.html">Registration</A>

</BODY>
</HTML>
