<HTML>
<HEAD><TITLE>RBckLib v1.0d Clipper Backup Library - Long Entry</TITLE></HEAD>
<BODY>

<A HREF="ng91c.html">[&lt;&lt;Previous Entry]</A>
<A HREF="ng20c.html">[^^Up^^]</A>
<A HREF="ng2de0.html">[Next Entry&gt;&gt;]</A>
<A HREF="menu.html" >[Menu]</A>
<A HREF="info.html">[About The Guide]</A>
<HR>
<PRE>
 <B>R_BackUp()</B>
 Creates a COMPRESSED BACKUP on one or more diskettes
───────────────────────────────────────────────────────────────────────────────

 <B>Syntax</B>
        nRetCode := R_Backup ( [ aSkeleton [, aExclude
                        [, cDestDrv [, cFileName
                        [, aBar
                        [, lEscape [, lSilent [, bRBckBlk
                        [, aMsg
                        ] ] ] ] ] ] ] ] ] )

 <B>Arguments</B>
        aSkeleton       Array with FILE SKELETONS of files to backup
                                p.e. { '*.*' } or { '*.DBF', 'C:\DB\*.NTX' }

                        A skeleton may contain a DRIVE and/or a PATH and
                        the DOS wildcards '*' and '?'.

                        If NO DRIVE is included the DEFAULT DRIVE will be
                        assumed; if NO PATH is included the DEFAULT PATH
                        will be assumed.

                        Default : <B>{ '*.*' }</B>
                                  <B>= ALL files on the current DRIVE and</B>
                                  <B>  DIRECTORY</B>
                        ─────────────────────────────────────────────────────
        aExclude        Array with FILE SKELETONS of files to EXCLUDE
                                from the backup
                                p.e. { '*.TMP' } or { '*.BAK', 'C:\DB\*.OBJ' }

                        A skeleton may contain a DRIVE and/or a PATH and
                        the DOS wildcards '*' and '?'.

                        If NO DRIVE is included the DEFAULT DRIVE will be
                        assumed; if NO PATH is included the DEFAULT PATH
                        will be assumed.

                        Default : <B>NIL = No exclusions</B>
                                  Backup all files as specified by aSkeleton
                        ─────────────────────────────────────────────────────
        cDestDrv        DESTINATION DRIVE for backup : 'A:', 'B:', ....

                        Default : <B>'A:'</B>
                        ─────────────────────────────────────────────────────
        cFileName       FILE NAME of the backup file(s) (w/o extension)

                        Notes   : - Maximal string length is 8 characters.

                                  - The EXTENSION of the backup file name is
                                    a SEQUENCE NUMBER ( '.000', '.001', ...)
                                    that is generated by RBackUp().

                        Default : <B>'@RBackUp'</B>
                        ─────────────────────────────────────────────────────
        aBar[5]         Array containing the PROGRESS BAR definition
                        (The BAR_* constants are pre-defined in <B>RBckLib.ch</B>)

                        aBar[1]         LENGTH of the progress bar
                        aBar[BAR_LEN]   Range   : 0 to MaxCol()
                                        Default : <B>0 = NO PROGRESS BAR</B>

                        aBar[2]         Screen ROW for the progress bar
                        aBar[BAR_ROW]   Range   : 0 to MaxRow()
                                        Default : <B>Centered</B>

                        aBar[3]         Screen COLUMN for the progress bar
                        aBar[BAR_COL]   Range   : 0 to MaxCol()-aBar[1]
                                        Default : <B>Centered</B>

                        aBar[4]         CHARACTER used for displaying the
                        aBar[BAR_CHR]   progress bar
                                        Default : <B>█ = Chr (219)</B>

                        aBar[5]         COLOR of the progress bar
                        aBar[BAR_CLR]   (Clipper color string)
                                        Default : <B>'GR+/G' = Yellow on Green</B>
                        ─────────────────────────────────────────────────────
        lEscape         .F.     : R_BackUp() can't be interrupted by the user
                        .T.     : R_BackUp() may be interrupted by pressing
                                  the &lt;Esc&gt;-key

                        Default : <B>.F. = &lt;Esc&gt;-key DISABLED</B>
                        ─────────────────────────────────────────────────────
        lSilent         .T.     : No alert sounds
                        .F.     : Give a beep when displaying 'INSERT DISK'
                                  message

                        Default : <B>.F. = Give a beep</B>
                        ─────────────────────────────────────────────────────
        bRBckBlk        CodeBlock that is evaluated once for each file, just
                        before it is added to the backup.

                        Notes   : - The CURRENT FILE NAME is passed to
                                    bRBckBlk (Format : d:\path\filename.ext)

                                  - bRBckBlk, when used, should return one of
                                    the following values :

                                    <B>0 = BU_ABORT</B>
                                        Backup process will be ABORTED.
                                        In this case, R_Backup() returns
                                        the <B>BU_USER_ABORT</B> return code

                                    <B>1 = BU_CONT</B>
                                        Backup process will CONTINUE.

                                    (These constants are pre-defined in
                                     <B>RBckLib.CH</B>)

                        Default : <B>{ || BU_CONT }</B>
                        ─────────────────────────────────────────────────────
        aMsg[4]         Array containing definition of the 'INSERT NEXT DISK'
                        message.
                        (The MSG_* constants are pre-defined in <B>RBckLib.ch</B>)

                        aMsg[1]         ROW for displaying the 'INSERT NEXT
                        aMsg[MSG_ROW]   DISK'-message
                                        Range   : 0 to MaxRow()
                                        Default : <B>Centered</B>

                        aMsg[2]         Screen COLUMN for displaying the
                        aMsg[MSG_COL]   'INSERT NEXT DISK'-message
                                        Range   : 0 to MaxCol()-Len(aMsg[3])
                                        Default : <B>Centered</B>

                        aMsg[3]         'INSERT NEXT DISK'-message to display
                        aMsg[MSG_TXT]   Message that will be displayed when
                                        the user has to insert the next
                                        backup disk.
                                        The DISK NUMBER is generated by
                                        R_BackUp().
                                        Insert '<B>XXX</B>' in the aMsg[3]-string
                                        on the position where the <B>DISK NUMBER</B>
                                        should be substituted.
                                        P.e. :
                                        'Please insert disk #XXX into A:'
                                        On runtime the XXX will be replaced
                                        with the current disk number.
                                        Default :
                                        If lEscape = .T.
               <B>'Insert backup disk # XXX into drive d:  &lt;─┘=Okay, Esc=Cancel'</B>
                                        If lEscape = .F.
               <B>'Insert backup disk # XXX into drive d:  Press &lt;─┘ when ready'</B>
                                        (For d:, the actual backup drive
                                        will be substituted)

                        aMsg[4]         COLOR used to display the 'INSERT
                        aMsg[MSG_CLR]   NEXT DISK'-message
                                        (Clipper color string)
                                        Default : <B>'W+*/B' = Blinking Bright</B>
                                                            <B>White on Blue</B>
                        ─────────────────────────────────────────────────────

 <B>Returns</B>
        nRetCode        Return code

                        Possible return codes :

                         0 = BU_OKAY           No errors detected
                         1 = BU_INVALID_PARM   Invalid parameter(s) passed
                         2 = BU_OPEN_INPUT     Error opening  input  file
                         4 = BU_CREATE_OUTPUT  Error creating output file
                         5 = BU_READ_INPUT     Error reading  input  file
                         6 = BU_WRITE_OUTPUT   Error writing  output file
                         7 = BU_NO_FILES_FOUND No files found to backup
                         8 = BU_USER_ABORT     Function aborted by user
                         9 = BU_INVALID_DRIVE  Invalid backup drive/disk

                        See also the <B>ErrorCodes</B>-section in this guide.

 <B>Description</B>
        Creates a COMPRESSED backup of the specified files.

        ┌────────────────────────────────────────────────────────────────╖
        │                        -+- <B> CAUTION </B> -+-                       ║
        │                                                                ║
        │   <B> ALL FILES IN THE ROOT DIRECTORY OF THE DESTINATION DISK </B>    ║
        │   <B>                     WILL BE DELETED !!                  </B>    ║
        │                                                                ║
        ╘════════════════════════════════════════════════════════════════╝

        Features :
        - Compression of the files that are backed up
        - (Optional) progress bar display
        - Multiple disk backup
        - User-definable messages

        Notes :
        - Make sure that the files to be backed up are closed :
          dbCloseAll (); dbCommitAll ()
        - Empty files ( = files with filesize = 0) are not backed up !

 <B>Example</B>
        *-- Header file of RBckLib
        #include "RBckLib.CH"

        LOCAL   nRetCode                        &amp;&amp; Return code R_BackUp()
        LOCAL   aBckError := BU_ERRMSG          &amp;&amp; Array with error msgs

        *------------------------------------------------------------------
        * Files to be backed up : 1) 'C:\SYSTEM1\*.DBF'
        *                         2) '*.DBF' and '*.NTX' in the current
        *                            directory on the current drive
        * Exclude               : 'C:\SYSTEM1\TEST.DBF'
        * Destination drive     : 'B:'
        * Backup file name      : '@RBackUp' (default)
        * Progress bar          : Length    = 30
        *                         Row       = Centered (default)
        *                         Column    = Centered (default)
        *                         Character = '#'
        *                         Color     = 'GR+/R'
        * Interruptable         : lEscape   = .T.
        * CodeBlock for name    : { |fname| ShowName ( fname ) }
        * Insert disk message   : Row       = 24
        *                         Column    = Centered (default)
        *                         Msg Text  = Default message
        *                         Msg Color = 'W+*/B'  (default)
        *------------------------------------------------------------------
        nRetCode := <B>R_BackUp</B> ( { 'C:\SYSTEM1\*.DBF', '*.DBF', '*.NTX' }, ;
                        { 'C:\SYSTEM1\TEST.DBF' }, ;
                        'B:', nil, { 30, nil, nil, '#', 'GR+/R' }, ;
                        .T., nil, { |fname| ShowName ( fname ) }, ;
                        { 24, nil, nil, nil } )

        IF nRetCode != BU_OKAY
                *-- Oops, error while backupping ...

                *-- Display the error message text
                ALERT ( 'Backup error : ' + aBckError [ nRetCode ] )
        ENDIF

        *-- Continuation of the program
        [...]

        QUIT

        *------------------------------------------------------------------
        * Display the name of the file being backed up
        *------------------------------------------------------------------
        FUNCTION ShowName ( cFileName )

        DevPos ( 14, 0 )
        DevOut ( 'Processing : ' + PadR ( cFileName, 40 ) )

        RETURN  BU_CONT                 &amp;&amp; Continue backup

        More examples can be found in the demo program <B>RBckDemo.PRG</B>.

 <B>Caution</B>

        ┌────────────────────────────────────────────────────────────────╖
        │                         -+- CAUTION -+-                        ║
        │                                                                ║
        │     IN THE UN-REGISTERED  VERSION OF RBCKLIB THE FOLLOWING     ║
        │     REGISTRATION MESSAGE WILL BE DISPLAYED AT RANDOM TIMES     ║
        │          (WHILE EXECUTING THE R_BackUp() FUNCTION) :           ║
        │                                                                ║
        │ <B>UN-Registered version of RBckLib -+- Please wait for 9 seconds</B> ║
        │                                                                ║
        │       AFTER A 9 SECONDS DELAY THE PROGRAM WILL CONTINUE.       ║
        ╘════════════════════════════════════════════════════════════════╝

 <B>о</B>
</PRE>
<HR>
<B>See Also:</B>
<A HREF="ng2de0.html">R_Restore()</A>
<A HREF="ng54bb.html">R_BckList()</A>
<A HREF="nge7bd.html">ErrorCodes</A>
<A HREF="ngfe70.html">Registration</A>

</BODY>
</HTML>
