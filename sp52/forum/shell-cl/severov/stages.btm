@echo off
cls
text
              ╔════════════════════════════════════════════════╗
              ║            Д У Б Л И Р О В А Н И Е             ║
              ╚════════════════════════════════════════════════╝
endtext
iff .%1==. then
text
                         Павел Северов  1992

      обращение: STAGES ЧАСТ ПОК ПУТЬ ДУБЛЬ [опции архиватора ARJ]

      ЧАСТ - Как часто делать полное дублирование данных (в днях).
             Если ЧАСТ=0, то дублирование ТОЛЬКО полное, если ЧАСТ=1,
             то полное дублирование производится один раз в день, а все
             промежуточные дубли - только свежих файлов, если ЧАСТ=2,
             то полное дублирование - раз в два дня и т.д.
             Основное правило программы - на устройстве должен сохраняться
             хотя бы один полный дубль.

      ПОК  - Максимальное число поколений дублей (цифра),
             Программа поддерживает не более ПОК дублей на устройстве.
             Если ПОК=0 - сколько влезет в устройство.

      ПУТЬ - путь к устройству/директории, где накапливаются дубли;

      ДУБЛЬ - первые три буквы файлов-дублей.
endtext
quit
endiff

text
      ╔══════════════════════════════════════════════════════════════════╗
      ║     Для успешной работы на компьютере следует  регулярно дублиро-║
      ║ вать обновляемую Вами информацию. Жесткий диск - ненадежное место║
      ║ для постоянного хранения Ваших файлов. Компьютерные  вирусы, тех-║
      ║ нические поломки, Ваше некорректное обращение с программами могут║
      ║ привести к уничтожению ценной информации. Только многократно про-║
      ║ дублированные на нескольких гибких дисков данные могут считаться ║
      ║ надежно защищенными от капризов судьбы.                          ║
      ║                         ┌────────────┐                           ║
      ║                         │ ЗАПОМНИТЕ! │                           ║
      ║                         └────────────┘                           ║
      ║              Информация - это Ваше ВРЕМЯ и ДЕНЬГИ!               ║
      ╚══════════════════════════════════════════════════════════════════╝
endtext
menu 18 27 7 7 112 7 "Итак!" "Запустить дублирование" "Дублирование отменяется"
if errorlevel==2 quit
if errorlevel==0 quit

setlocal
set fre=%1
set sta=%2
if %sta==0 set sta=999
set pat=%3
set dup=%@SUBSTR[%4___,0,3]
set opt=%5&
set fil=$$new$$.arj
set dir=%pat\%dup?????.arj

:FORWARD
del %fil >& nul
if %@removable[%pat]==1 gosub MOUNTING
gosub INFO
if %count==0 set fre=0
text
            ╔════════════════════════════════════════════════════╗
            ║                                                    ║
            ║          Ждите, идет упаковка свежего дубля        ║
            ║                                                    ║
            ╚════════════════════════════════════════════════════╝
endtext

:PACKING
iff %lastn GE %fre then
  arj a %fil %opt >& nul
  set typ=F
else
  set d=%@FILEDATE[%lasta]
  set t=%@FILETIME[%lasta]
  set d=%@substr[%d,6,2]%@substr[%d,3,2]%@substr[%d,0,2]
  set t=%@substr[%t,0,2]%@substr[%t,3,2]59
  arj a %fil -o%[d]%[t] %opt >& nul
  set typ=N
  endiff


iff not exist %fil then
cls
iff errorlevel==0 then
text
          ╔════════════════════════════════════════════════════════╗
          ║                 Нечего дублировать!                    ║
          ╠════════════════════════════════════════════════════════╣
          ║ Со времени последнего дублирования файлы не изменялись.║
          ╚════════════════════════════════════════════════════════╝
endtext
gosub OK
goto QUIT
endiff
text
          ╔════════════════════════════════════════════════════════╗
          ║               Не могу создать дубль!                   ║
          ║    Возможно на диске не осталось места для упаковки.   ║
          ╠════════════════════════════════════════════════════════╣
          ║                Дублирование невозможно.                ║
          ║                 Разберитесь сами или                   ║
          ║        обратитесь к обслуживающему персоналу           ║
          ║                   и попробуйте снова.                  ║
          ╚════════════════════════════════════════════════════════╝
endtext
gosub OK
goto QUIT
endiff

set size=%@filesize[%fil,b]

:LOOPSTAGES
  if %count LT %sta goto REMOVE
  gosub OLDEST
  del %retval >& nul
  if exist %retval goto ERRDEV
  set f=%retval
  gosub COUNTMINUS
  goto LOOPSTAGES

:REMOVE
  if %@diskfree[%pat,b] GT %size goto CHECKING
  gosub OLDEST
  del %retval >& nul
  if exist %retval goto ERRDEV
  set f=%retval
  gosub COUNTMINUS
  goto REMOVE

:CHECKING
iff %[typ]%[fulld]==N0 then
  set fre=0
  set lastn=0
  goto PACKING
  endiff

text
            ╔════════════════════════════════════════════════════╗
            ║                                                    ║
            ║            Ждите, идет сохранение дубля!           ║
            ║                                                    ║
            ╚════════════════════════════════════════════════════╝
endtext

iff %count NE 0 then
  set m=%@SUBSTR[%@EVAL[1%@SUBSTR[%@NAME[%lasta],3,4]+1],1,4]
else
  set m=0001
  endiff

move %fil %pat\%[dup]%[m]%[typ].* >& nul
if not exist %pat\%[dup]%[m]%[typ].* goto ERRDEV

cls
text
             ╔═════════════════════════════════════════════════════╗
             ║             Дублирование прошло успешно.            ║
             ╚═════════════════════════════════════════════════════╝
endtext
gosub INFO
gosub OK
:QUIT
endlocal
quit

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:MOUNTING
iff %@ready[%pat]==0 then
  cls
  echo          ╔══════════════════════════════════════════════════════════╗
  echo          ║                                           ┌───────────┐  ║
  echo          ║  Вставьте дискету с дублями в накопитель  │ ═══:%pat═══ │  ║
  echo          ║                                           └───────────┘  ║
  echo          ╚══════════════════════════════════════════════════════════╝
  keystack down
  menu 13 20 7 7 112 7 "" "" "   После этого нажмите ENTER  " ""
endiff

:EXIST
if %@ready[%pat]==0 goto MOUNTING
iff not exist %dir then
cls
text
          ╔════════════════════════════════════════════════════════╗
          ║        Эта дискета не содержит предыдущих дублей.      ║
          ║           Возможно Вы вставили не ту дискету.          ║
          ╚════════════════════════════════════════════════════════╝
endtext
menu 6 18 7 7 112 7 "" "Записать дубль на эту дискету" "Я ошибся, давай попробуем другую дискету" "Прервать дублирование"
if errorlevel==1 return
if errorlevel==3 goto QUIT
if errorlevel==0 goto QUIT
goto MOUNTING
endiff
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:INFO
set count=0
set fulld=0
set lastf=
set lasta=
set timelf=0
set timela=0
set lastn=0
if not exist %dir return
for %f in (%dir) do gosub COUNTPLUS
set lastn=%@eval[%@date[%_date]-%@date[%@filedate[%lastf]]]

echo                       На дискете записано дублей: %count
echo                             из них полных: %[fulld]
gosub OLDEST
echos               самый старый дубль
echos   %@eval[%@date[%_date]-%@date[%@filedate[%retval]]]
echo -дневной давности (%@filedate[%retval])
:echo             самый новый полный дубль %lastn-дневной давности (%@filedate[%lastf])
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:COUNTPLUS
set count=%@EVAL[%count+1]
set timef=%@DATE[%@FILEDATE[%f]]%@FILETIME[%f]
iff %timef GT %timela then
  set lasta=%f
  set timela=%@DATE[%@FILEDATE[%lasta]]%@FILETIME[%lasta]
  endiff
iff %@upper[%@substr[%@name[%f],7,1]]==F then
  set fulld=%@EVAL[%[fulld]+1]
  iff %timef GT %timelf then
    set lastf=%f
    set timelf=%@DATE[%@FILEDATE[%lastf]]%@FILETIME[%lastf]
    endiff
  endiff
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:COUNTMINUS
set count=%@EVAL[%count-1]
if %@upper[%@substr[%@name[%f],7,1]]==F set fulld=%@EVAL[%[fulld]-1]
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:OLDEST
set m=%lasta
if %@len[%m] LT 3 for %f in (%dir) do set m=%f
for %f in (%dir) do if %@DATE[%@FILEDATE[%f]]%@FILETIME[%f] LT %@DATE[%@FILEDATE[%m]]%@FILETIME[%m] set m=%f
set retval=%m
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:OK
keystack down
menu 13 27 7 7 112 7 "" "" "   Нажмите ENTER  " ""
return

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:ERRDEV
cls
text
           ╔════════════════════════════════════════════════════════╗
           ║          На эту дискету дублирование невозможно.       ║
           ║            (Вероятно она защищена от записи.)          ║
           ╚════════════════════════════════════════════════════════╝
endtext
menu 7 23 7 7 112 7 "" "Сейчас я заменю дискету на другую" "Прервать дублирование"
if errorlevel==2 goto QUIT
if errorlevel==1 goto FORWARD
if errorlevel==0 qoto QUIT

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
