(****************************************************************************)
(*                       MPMENU.DEF 2.00                                    *)
(*                    Modula Plus Tools 2.00                                *)
(*                  Copyright (c) Holofax 1992.                             *)
(*              All rights reserved. Ukraine. Kiev.                         *)
(****************************************************************************)

(*$G+*)
DEFINITION MODULE MpMenu;
(*╔════════════════════════════════════════════════════════════════════════╗*)
(*║                     Модуль работы с "МЕНЮ".                            ║*)
(*╚════════════════════════════════════════════════════════════════════════╝*)


FROM MpWindow IMPORT pWindow;
FROM MpCrt    IMPORT GetKey,Pressed,KeyHome,KeyEnd,KeyUp,KeyDown,KeyPgUp,
                     KeyPgDn,KeyENTER,KeyESC,KeyLeft,KeyRight;
TYPE
  CommandMenu =
   (Mnone,          (* Нет команды                                           *)
    Mnext,          (* Следующий пункт в текущем меню                        *)
    Mprev,          (* Предыдущий пункт в текущем меню                       *)
    Mhome,          (* Первый пункт в текущем меню                           *)
    Mend,           (* Последний пункт в текущем меню                        *)
    Mctrl,          (* Эмуляция комманды Mselect для пункта, с упр. символом.*)
    MsubNext,       (* Следующий пункт в вызывающем меню                     *)
    MsubPrev,       (* Предыдущий пункт в вызывающем меню                    *)
    Mselect,        (* Войти в подменю (если есть)                           *)
    Mclose,         (* Закрыть текущее меню и выйти из него                  *)
    Mexit,          (* Выйти из текущего меню                                *)
    Muser);         (* Экстренный выход из UseMenu.                          *)
  CommandMenuSet = SET OF CommandMenu;

  Command = ARRAY[0..255] OF CommandMenuSet;

TYPE
  HelpProcMenu = PROCEDURE ((*ID*)CARDINAL);

TYPE
  MenuColors = RECORD    (* Цвета меню                                      *)
    ItemFore  : CARDINAL;(* Цвет символов пункта меню                       *)
    ItemBack  : CARDINAL;(* Цвет фона пункта меню                           *)
    HideFore  : CARDINAL;(* Цвет текста невыбираемого пункта                *)
    HideBack  : CARDINAL;(* Цвет фона   невыбираемого пункта                *)
    ActivFore : CARDINAL;(* Цвет текста активного пункта                    *)
    ActivBack : CARDINAL;(* Цвет фона   активного пункта                    *)
    ActChFore : CARDINAL;(* Цвет контрольного символа активного пункта      *)
    ActChBack : CARDINAL;(* Цвет фона контрольного символа активного пункта *)
    ChFore    : CARDINAL;(* Цвет контрольных символов меню                  *)
    ChBack    : CARDINAL;(* Цвет фона контрольных символов меню             *)
  END;

  pName     = POINTER TO ARRAY[0..255] OF CHAR;
  pItemMenu = POINTER TO ItemMenu;
  pMenu     = POINTER TO Menu;

  ItemMenu = RECORD      (* Пункт меню                                      *)
    ID       : CARDINAL; (* Идентификатор пункта                            *)
    X,Y      : CARDINAL; (* Координаты пункта в окне меню                   *)
    NextItem : pItemMenu;(* Указатель на следующий пункт                    *)
    PrevItem : pItemMenu;(* Указатель на предыдущий пункт                   *)
    CtrlCh   : CHAR;     (* Контрольный символ пункта                       *)
    Enabled  : BOOLEAN;  (* Выбираемый пункт                                *)
    Name     : pName;    (* Указатель на строку пункта                      *)
    Sub      : pMenu;    (* Указатель на подменю                            *)
  END;(*ItemMenu*)

  Menu = RECORD (* Меню *)
    Win     : pWindow;      (* Дескриптор окна                              *)
    First   : pItemMenu;    (* Указатель на первый пункт меню               *)
    Last    : pItemMenu;    (* Указатель на последний пункт меню            *)
    Current : pItemMenu;    (* Указатель на текущий пункт меню              *)
    Help    : HelpProcMenu; (* Процедура Help типа                          *)
    Keys    : Command;      (* Множество выполняемых команд                 *)
    Colors  : MenuColors;   (* Цвета                                        *)
    PrevMenu : pMenu;
  END;


VAR
  CurrentMenu : pMenu;

                  (* -------------------------------- *)
                  (* Основные процедуры работы с меню *)
                  (* -------------------------------- *)

PROCEDURE MakeMenu(ax,ay,bx,by,                  (* Координаты окна       *)
                   ctext,cback,                  (* Цвет текста в окне    *)
                   cacttext,cactback : CARDINAL; (* Цвет активной записи  *)
                   expl : BOOLEAN) : pMenu;      (* Сохранять под окном ? *)
(* Создать меню.                                                            *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE SubMenu(menu : pMenu; iditem : CARDINAL; submenu : pMenu);
(* Вставить меню (submenu) в меню (menu) для пункта iditem, как подменю.    *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE MenuItem(VAR menu : pMenu; str : pName; xI,yI : CARDINAL;id : CARDINAL);
(* Создать пункт меню.                                                      *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ChangeItemS(menu : pMenu; str : pName; id : CARDINAL);
(* Изменить текст пункта.                                                   *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ChangeItemXY(menu : pMenu; xI,yI : CARDINAL; id : CARDINAL);
(* Изменить координаты пункта.                                              *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE EnableItem(menu : pMenu; id : CARDINAL; enab : BOOLEAN);
(* Пункт меню (menu) c номером id запретить (enab=FALSE), или разрешить.    *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE RingItemMenu(menu : pMenu);
(* Замкнуть пункты меню в кольцо                                            *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ShowMenu(menu : pMenu);
(* Открыть меню.                                                            *)
(* ════════════════════════════════════════════════════════════════════════ *)


PROCEDURE UseMenu(VAR key : CHAR) : CARDINAL;
(* Выбор в меню. Возвращает последнюю нажатую клавишу(key) и номер записи.  *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE HideMenu(menu : pMenu);
(* Закрыть меню (menu).                                                     *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE HideCurrentMenu();
(* Закрыть текущее меню.                                                    *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE HideAllMenu();
(* Закрыть всю ветвь меню.                                                  *)
(* ------------------------------------------------------------------------ *)

PROCEDURE KillMenu(VAR menu : pMenu);
(* Удалить меню из памяти (уничтожить).                                     *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE MenuWin(menu : pMenu) : pWindow;
(* Возвратить дескриптор окна меню.                                         *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ReplaceMenu(menu : pMenu; x,y : CARDINAL);
(* Изменяет положение меню (без подменю) на экране.                         *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ResizeMenu(menu : pMenu; wid,dep : CARDINAL);
(* Изменяет размер окна меню (без подменю) вправо и вниз.                   *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE SetColorsMenu(menu : pMenu;
                           if,  (* Ц. символов пункта                       *)
                           ib,  (* Ц. фона пункта                           *)
                           hf,  (* Ц. текста невыбираемого пункта           *)
                           hb,  (* Ц. фона   невыбираемого пункта           *)
                           af,  (* Ц. текста активного пункта               *)
                           ab,  (* Ц. фона   активного пункта               *)
                           acf, (* Ц. контр. символа активного пункта       *)
                           acb, (* Ц. фона контр. символа активного пункта  *)
                           cf,  (* Ц. контрольных символов меню             *)
                           cb : (* Ц. фона контрольных символов меню        *)
                                CARDINAL);

PROCEDURE GetCurrentMenu() : pMenu;
(* Возвращает указатель на текущее меню                                     *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE SetCurrentMenu(menu : pMenu);
(* Устанавливает текущее меню                                               *)
(* ════════════════════════════════════════════════════════════════════════ *)

              (* -------------------------------- *)
              (*              Команды             *)
              (* -------------------------------- *)

PROCEDURE InclMenuCommand(menu : pMenu; comand : CommandMenu; key : CHAR);
(* Добавить команду меню.                                                   *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ExclMenuCommand(menu : pMenu; comand : CommandMenu; key : CHAR);
(* Убрать команду меню.                                                     *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE ClrMenuCommandSet(menu : pMenu; key : CHAR);
(* Удалить все множество команд mеню для клавиши key.                       *)
(* ════════════════════════════════════════════════════════════════════════ *)


PROCEDURE ShowItemsMenu(menu : pMenu);
(* Вывести пункты меню.                                                     *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE SetHelpMenu(m : pMenu; help : HelpProcMenu);
(* Установить процедуру контекстно зависимой помощи.                        *)
(* ════════════════════════════════════════════════════════════════════════ *)

PROCEDURE DelHelpMenu(m : pMenu);
(* Удалить процедуру контекстно зависимой помощи.                           *)
(* ════════════════════════════════════════════════════════════════════════ *)


PROCEDURE FindNextItem(item : pItemMenu) : pItemMenu;

PROCEDURE FindPrevItem(item : pItemMenu) : pItemMenu;

PROCEDURE FindItemWithCtrlCh(menu : pMenu; ch : CHAR) : pItemMenu;

PROCEDURE FindItemID(menu : pMenu; id : CARDINAL) : pItemMenu;

END MpMenu.
