
;------------------------------------------------
; Модуль перехватчика прерывания INT 10
; к программе AntiGame  V 1.0
;------------------------------------------------
; Язык программирования :
; Macro Assembler V 5.1
;------------------------------------------------
; Дата последних изменений
; 19.03.1992
;------------------------------------------------
; (c) 1992, Мигач Ярослав
;------------------------------------------------

EXTRN   FINISH : FAR   ; Подрограмма вывода предупреждающего
                       ; сообщения и снятия задачи

DATA    SEGMENT PUBLIC

        ASSUME DS : DATA

EXTRN   INT10 : DWORD  ; Сохраненное значение базового
                       ; вектора прерывания INT 10

DATA    ENDS

CODE    SEGMENT PUBLIC

        ASSUME  CS : CODE

SAVEDS    DW      ?    ; Значение сегмента DS устанавливаемое
                       ; подпрограммой инициализации

COUNT     DB      ?    ; Значение счетчика санкционированного
                       ; доступа

        PUBLIC  ANALIZ
        PUBLIC  InitDs

;---------------------------------------------------------
; Процедура перехвата вектора INT 10
;---------------------------------------------------------
; При нулевом значении COUNT и ( AH = 0 )
; AND ( NOT ( AL IN [ 0, 1, 2, 3 ] ) ) вызывает
; процедуру вывода сообщения и снятия задачи
; При ненулевом значении COUNT и ( AH = 0 )
; AND ( NOT ( AL IN [ 0, 1, 2, 3 ] ) ) уменьшает
; значение COUNT и вызывает базовый обработчик
; При AH = 0EEh воспринимает AL как новое значение
; счетчика COUNT
; При других значениях регистров, просто вызывает
; базовый обработчик
;---------------------------------------------------------

ANALIZ  PROC    FAR

        ; Устанавливаем необходимое значение DS

        PUSH    DS
        PUSH    AX
        MOV     AX, SAVEDS
        MOV     DS, AX
        POP     AX

        PUSHF

        ; Анализируем установку счетчика
        ; санкционированного достыпа

        CMP     AH, 0EEH
        JNZ     GOCHECK
        MOV     COUNT, AL

        POPF
        POP     DS
        IRET

GOCHECK :

        ; Для всех значений AH <> 0  и для
        ; AH = 0 и  AL = 1, 2, 3 вызываем
        ; базовый обработчик

        CMP     AH, 0
        JNZ     LBL1
        CMP     AL, 0
        JZ      LBL1
        CMP     AL, 1
        JZ      LBL1
        CMP     AL, 2
        JZ      LBL1
        CMP     AL, 3
        JZ      LBL1

        ; Идентификация санкционированного
        ; доступа

        PUSH    AX
        MOV     AL, COUNT
        OR      AL,AL
        JZ      GOFN
        DEC     AL
        MOV     COUNT, AL
        POP     AX
        JMP     LBL1

GOFN :

        ; Иначе выдаем сообщение и снимаем
        ; вызывающую задачу

        CALL    FINISH

        ; Устанавливаем стек и вызываем
        ; базовый обработчик

LBL1 :  POPF
        PUSHF

        CALL    INT10

        POP     DS
        IRET

ANALIZ  ENDP

        ; Начальная инициализация сегмета DS
        ; и счетчика санкционированного доступа

INITDS  PROC    NEAR

        PUSH    AX
        MOV     AX, DS
        MOV     SAVEDS, AX

        MOV     AL, 0
        MOV     COUNT, AL

        POP     AX
        RET

INITDS  ENDP

CODE    ENDS

        END
