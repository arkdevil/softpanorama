                  КОММЕНТАРИИ К ФАЙЛУ SSD.BAT

    ( Предполагается, что Вы уже прочитали ..\Sped_SD.txt и
                  ..\!DirRoom\!DirRoom.doc )

    SSD.bat запускает Speed Disk, резервируя место под оглавления
каталогов и удаляя с диска файлы treeinfo.ncd, Adinf═?═.░░░ и
Image.?a?.
    Предполагается, что Speed Disk не переименован - SpeeDisk;
SpeeDisk и !DirRoom доступны в Path.

     Запуск файла:
                     SSD  Disk: [SaveDir]
    где Disk: - обрабатываемый диск (буква с двоеточием, ре-
                                             гистр не важен);
        SaveDir (необязательный параметр) - директорий для хра-
нения во время оптимизации файла Adinf═?═.░░░ .

                          ТЕКСТ ФАЙЛА

  | @echo off
 1| if .%2.==.. %0 %1 G:\
  | echo SSD - Speed SpeeDisk,  edition 12-11-92.   (C) V.S. Rabets, 1992.
  | echo.
 2| for %%f in ( A: a: B: ........ H: h: ) do if .%%f.==.%1. goto Continue
  | echo Неверно задан диск.
  | goto END
  |
  | :Continue
 3| %1
 4| cd \
  |
 5| FA SD.ini       /clear
 6| FA Adinf═?═.░░░ /clear
 7| FA Image.?a?    /clear
 8| FA !DirRoom     /clear
  |
 9| del      %2Adinf═?═.░░░
10| copy /b    Adinf═?═.░░░ %2
11| if exist %2Adinf═?═.░░░ del Adinf═?═.░░░
12| del           Image.?a?
13| del        treeinfo.ncd
  |
14| for %%f in ( C: c: ) do if %%f==%1  !DirRoom %1 /p 60
15| for %%f in ( D: d: ) do if %%f==%1  !DirRoom %1 /p 120
16| for %%f in ( E: e: ) do if %%f==%1  !DirRoom %1 /p 200
17| for %%f in ( F: f: ) do if %%f==%1  !DirRoom %1 /p 90
  |
18| SpeeDisk %1
  |
19| copy /b %2Adinf═?═.░░░ %1
20| FA SD.ini       /HID+
21| FA Adinf═?═.░░░ /HID+ /R+
22| FA !DirRoom     /HID+
  |
23| Image %1
  |
  | :END

    В строке 1 подставляется параметр SaveDir по умолчанию, ес-
ли он не указан в командной строке ( если SaveDir не указан, то
bat-файл вызывает себя же, но уже с SaveDir в командной строке.
Это позволяет вызывать SSD, указывая только диск:  SSD C: ).
В моем случае SaveDir - это RAM-диск достаточной емкости G:\ .
В этой строке G:\ нужно заменить на Ваше значение.
    В строке 2 проверяется правильность первого параметра - дис-
ка. Изначально SSD.bat настроен на машину с дисками от A: до H:,
из которых жесткие - от C: до F: . Строку 2 можно подредактиро-
вать исходя из конфигурации Вашей машины.
    Строки 3-4 - переход в корневой каталог обрабатываемого
диска.
    Затем удаляются файлы из корневого каталога:
     treeinfo.ncd - этот файл нужно удалить обязательно, т.к.
       его размер может меняться. Treeinfo.ncd быстро восста-
       навливается при первом обращении из Norton Commander'а к
       дереву, поэтому он просто вычеркивается, нигде не сохра-
       няясь (строка 13).
     Image.?a?, т.е. Image.dat и Image.bak. Т.к. Image будет
       запущен сразу же после SpeeDisk'а, то Image.?a? просто
       вычеркиваются, нигде не сохраняясь (строка 12).
       Image.idx расположен в самом конце диска, поэтому опти-
       мизации не мешает и его можно не трогать.
     Adinf═?═.░░░ - этот файл, с одной стороны, нужно сохранить,
       с другой стороны - удалить с обрабатываемого диска, т.к.
       размер его может меняться. Он переносится в SaveDir (па-
       раметр %2) в строках 9-11. Обратите внимание, что
       Adinf═?═.░░░ стирается с диска, только если он успешно
       скопирован в SaveDir (строка 11, if exist...) - т.е.
       лучше медленная оптимизация, чем потеря Adinf═?═.░░░.
     Файл Sd.ini необходимо сохранить в корневом каталоге. Т.к.
       размер его постоянный, то он не нарушит компоновку дис-
       ка. В строке 5 с него снимается атрибут hidden (см.
       Speed_SD.txt). Так же можно было бы поступить и с файлом
       Image.dat, чей размер тоже фиксирован.
    Строки 6-8 - снятие лишних атрибутов с удаляемых файлов.
    Строки 14-17 - вызов !DirRoom. Последнее число в каждой из
этих строк - размер области, резервируемой под каталоги. Эти
строки нужно настроить под конкретные диски. Перед настройкой
запустите !DirRoom в информационном режиме и выясните текущий
объем каталогов, например, !DirRoom C: . Это значение следует
вставить в SSD.bat с некоторым запасом. При устоявшейся струк-
туре диска полуторакратного запаса очень достаточно.
Если диск достаточно медленный, можно в эти строки вставить па-
раметр /V для вывода списка каталогов. На быстром диске список
толком и не разглядишь.
    Строка 18 - вызов Speed Disk'а. Можно было бы вставить туда
параметры, определяющие режим оптимизации. Но, как мне кажется,
удобнее установить постоянно Full Optimization with DIR's first,
а примерно раз в неделю в интерактивном режиме переключаться
на Full Optimization with File reorder.
    Строки 19-22 - возвращение на место Adinf═?═.░░░ и возвра-
щение файлам их прежних атрибутов.
    Строка 23 - восстановление Image.dat.

    Т.о., ДЛЯ НАСТРОЙКИ SSD.bat на конкретную машину нужно от-
редактировать строки 1, 2 и 14-17.

    НЕ ЗАБУДЬТЕ в Speed Disk'е ВНЕСТИ ФАЙЛ !DirRoom ПЕРВЫМ в
список Files to place first и записать эту конфигурацию на диск
(Save options to disk).


         11-11-92                    В.С. Рабец

                           e-mail:   rabets@icph20.sherna.msk.su

                           Адрес:    142 432
                                     Московская обл.
                                     Ногинский р-н
                                     п. Черноголовка
                                     Школьный б-р, 18, кв. 241
                                     Рабцу В.С.
