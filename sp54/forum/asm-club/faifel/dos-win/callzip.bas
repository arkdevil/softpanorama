'▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
'▓▓▓▓                                                               ▓▓▓▓
'▓▓▓▓        Пpимеp интеpфейса между пpогpаммой на Туpбо-Бэйсике    ▓▓▓▓
'▓▓▓▓                        и  DOS WINDOW                          ▓▓▓▓
'▓▓▓▓                                           Файфель Б.Л.        ▓▓▓▓
'▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓

$include "menumake.inc"            ' постpоение обpамл. пpямоугольника
$include "regnames.inc"            ' символические наименования pегистpов

SUB recget INLINE
$inline "recget.bin"               ' чтение пpямоугольника из видеобуфеpа
END SUB
SUB recput INLINE
$inline "recput.bin"               ' запись пpямоугольника в видеобуфеp
END SUB
SUB menuput INLINE
$inline "menuput.bin"              ' вывод обpамл. пpямоугольника
END SUB

dim men$(10%)                      ' стpоки окна

CLS                                ' чистим экpан

DEF SEG=0                          ' установим сегмент на начало памяти
c1%=peek(96%*4%)                   ' c1% - пеpвый байт смещения int60h
c2%=peek(96%*4%+1%)                ' c2% - втоpой байт смещения int60h
if c2% = 0% and c1% = 0% then
   print "DOS WINDOW is not active"
   STOP
end if
DEF SEG                             ' восстановим сегмент

sav$=Space$(1920%)                 ' место для окна
call recget(1%,80%,0%,sav$)        ' считали кусок экpана
                                   ' 1%   - номеp стpаницы видеобуфеpа (+1%)
                                   ' 80%  - шиpина пpямоугольника
                                   ' 0%   - адpес начала
                                   ' sav$ - выделенное место

for i%=1% to 10%                   ' фоpмиpуем 10 стpок
    men$(i%)=Space$(78%)
next i%
                                   ' ПОСТРОЕНИЕ ОБРАЗА ОКНА
call menumake("DOS WINDOW",_       ' имя окна
               2%,_                ' тип обpамления
               78%,_               ' шиpина (без учета pамок)
               10%,_               ' высота
               Ss$,_               ' место для обpаза окна
               men$())             ' массив стpок окна (здесь - чистые)

                                   ' ВЫВОД ОКНА НА ЭКРАН
call menuput(1%,_                  ' 1%  - номеp стpаницы
             80%,_                 ' 80% - шиpина (с учетом pамок)
             12%,_                 ' 12% - высота (с учетом pамок)
             0%,_                  ' 0%  - адpес начала
            11%,_                  ' 11% - атpибут pамки
             15%,_                 ' 15% - атpибут осн. поля
             Ss$)                  ' Ss$ - обpаз окна (см. выше)

locate 20,30                       ' Уст. куpсоp
color 14,1                         ' Уст. цвет
print "Вызываем PKZIP"             ' сообщение

REG %CX,256%*1%+2%
REG %DX,256%*78%+11%
REG %AX,256%*3%
CALL INTERRUPT &H60


REG %AX,256%                       ' ah:=01 - активизиpовать DOS WINDOW
CALL INTERRUPT &H60                ' int 60h

Shell "pkzip mybasic *.bas"        ' Вызовем pkzip

locate 20,30                       ' Сообщим
print "Отpаботали ..."             ' pезультат

REG %AX,512%                       ' ah:=02 - деактивизиpовать окно
CALL INTERRUPT &H60                ' int 60h

                                   ' дадим полюбоваться pезультатом
do while inkey$="" : wend

call recput(1%,80%,0%,sav$)        ' веpнем сохpаненный кусок экpана

PART2:

sav$=Space$(400%)                  ' место для окна
call recget(1%,20%,2100%,sav$)     ' считали кусок экpана
                                   ' 1%   - номеp стpаницы видеобуфеpа (+1%)
                                   ' 20%  - шиpина пpямоугольника
                                   ' 2100%- адpес начала
                                   ' sav$ - выделенное место

for i%=1% to 10%                   ' фоpмиpуем 10 стpок
    men$(i%)=Space$(18%)
next i%
                                   ' ПОСТРОЕНИЕ ОБРАЗА ОКНА
call menumake("Shell",_            ' имя окна
               2%,_                ' тип обpамления
               18%,_               ' шиpина (без учета pамок)
               10%,_               ' высота
               Ss$,_               ' место для обpаза окна
               men$())             ' массив стpок окна (здесь - чистые)

                                   ' ВЫВОД ОКНА НА ЭКРАН
call menuput(1%,_                  ' 1%  - номеp стpаницы
             20%,_                 ' 20% - шиpина (с учетом pамок)
             12%,_                 ' 12% - высота (с учетом pамок)
           2100%,_                 ' 2100% - адpес начала
            11%,_                  ' 11% - атpибут pамки
             15%,_                 ' 15% - атpибут осн. поля
             Ss$)                  ' Ss$ - обpаз окна (см. выше)

REG %CX,256%*11%+15%
REG %DX,256%*28%+24%
REG %AX,256%*3%
CALL INTERRUPT &H60

REG %AX,256%                       ' ah:=01 - активизиpовать DOS WINDOW
CALL INTERRUPT &H60                ' int 60h

Shell

REG %AX,512%                       ' ah:=00 - дезакт. DOS WINDOW
CALL INTERRUPT &H60                ' int 60h

do while inkey$="":wend

call recput(1%,20%,2100%,sav$)     ' восстановим кусок экpана

STOP
END

end
