
ShareWare product.

Если использование данного продукта представляет для вас интерес,  вы можете
стать его зарегистрированным пользователем. Двух рублей, пересланных по указанно-
му ниже адресу будет достаточно для сообщения автору, что работу над этим продук-
том следует продолжать. Кроме того вы сами можете стать его распространителем
пересылая автору 50 % поступлений. Если у вас трудности с деньгами - ваши заме-
чания по содержанию и по возможным ошибкам будут приняты с благодарностью.
Зарегистрированные пользователи будут ставиться в известность о дальнейших
дополнениях и изменениях продукта. Просьба не удалять данное сообщение при пере-
даче продукта своим знакомым и друзьям.


					Авторские права принадлежат
					Козловскому Юрию Константиновичу
					272630 Одесская обл.
					       г. Измаил
					       ул. Тимирязева, 9





	О П И С А Н И Е    М А К Р О С О В   Д Л Я    С Т Р У К Т У Р Н О Г О

			 П Р О Г Р А М М И Р О В А Н И Я

	  П Р И  И С П О Л Ь З О В А Н И И   М А К Р О А С С Е М Б Л Е Р А

			     I B M	     P C

	       г. И З М А И Л	  Январь 1990	  Ю.К.Козловский
			       ул. Тимирязева ,9



			    С О Д Е Р Ж А Н И Е


	1. Введение ................................................ 03

	2. Макроопределение IF$..................................... 04

	3. Макроопределение SWITCH.................................. 05

	4. Макроопределение DO...................................... 07

		4.1 Конструкция   DO  ENDDO ........................ 07

		4.2 Конструкция   DO  UNTIL ........................ 08

	5. Макроопределение FOR..................................... 09

	6. Макроопределение WHILE................................... 11

	7. Макроопределение CYCLE................................... 12

	8. Вложения макрокоманд .................................... 14

	9. Ошибки   ................................................ 16

	Приложение  1. Список возможных  отношений.

	Приложение  2. Содержание файла  макроопределений

       10. Дополнения............................................... 18

		10.1  Макроопределение	 IF$OR

		10.2  Макроопределение	 IF$AND

		10.3  Макроопределение	 WHILE$OR

		10.4  Макроопределение	 WHILE$AND

		10.5  Макроопределение	 UNTIL$OR

		10.6  Макроопределение	 UNTIL$AND
 


				    _3_


			   1. В В Е Д Е Н И Е


	Описаны правила использования пяти основных макроопределений,

и нескольких дополнительных (CYCLE , IF$OR , IF$AND ) макрокоманд,

позволяющих писать хорошо структуированные программы  с  минимальной

потерей эффективности выполнения задач. Написаны   и   отлажены  при

использовании	 Masm V5.0. Программист, знакомый  с языками програм-

мирования высокого уровня с удовольствием возьмет их себе на вооруже-

ние.

	Достоинством данных макрокоманд является отсутствие необходи-

мости вести нумерацию (  уникально идентифицировать каждый случай ис-

пользования макрорасширения, особенно при вложениях  ), а также  гиб-

кость конструкций в отношении формирования наиболее эффективного кода.

О накладных расходах. Ассемблирование полностью "структуированной"

программы замедляется в 1,2 - 1,8 раза.Придется отказаться от исполь-

зования меток типа $$Ann..nn:

	где A - одна из латинских букв A B C D E

	    n - цифры,зависящие от номера макрорасширения и уровня

		вложения;

	не следует также использовать символы типа

		com__c,lev__l,old__l	и.т.д.

	которые берут на себя заботу об идентификации макрорасширений.

	Если эти ограничения  не  показались вам слишком строгими, что

совсем не далеко от истины , буду рад принести ВАМ пользу.

	Расчитано на программиста уже имеющего хотя бы небольшой

опыт программирования на языке ассемблера.

				    _4_

		2. Макроопределение IF$
Синтаксис:
	IF$	reg rel arg	[L]

		[ последовательность операторов 1 ]
	ELSE$
		[ последовательность операторов 2 ]
	ENDIF$

Параметры:	1. reg - рабочий регистр
		2. rel - отношение сравнения,получающееся
		   отбрасыванием буквы J в соответствующей
		   команде перехода (LE E NB NGE и.т.д)
		3. arg - число (символ,регистр) для сравнения
		4. L - при наличии этого параметра происходит
		   формирование jmp (длинного) перехода.
		   Допустим любой символ .

Описание:	Сравнение происходит с помощью команды CMP.
		При истинности условия выполняется последова-
		тельность операторов 1, и последовательность
		операторов 2, при невыполнении условия	rel.
		При наличии параметра L  формируется JMP переход.
Пример:
	    a)	if$	ax e 3			b)  if$     ax e 3	   L
			mov	cx,1			    mov     cx,1
		else$				    else$		   L
			mov	cx,0			    mov     cx,0
		endif$				    endif$
Листинг:
		cmp	ax,3				cmp	ax,3
		jne    $$B1el				je     $$B2f
		mov	cx,1				jmp	$$B2el
		jmp	short $$B1e		$$B2f:
       $$B1el:						mov	cx,1
		mov	cx,0				jmp	$$B2e
       $$B1e:					$$B2el:
							mov	cx,0
						$$B2e:
	c)	Укороченный вариант

		if$	cx nge 4			cmp	cx,4
			xor	ax,ax			jge  $$B1el
		endif$					xor	ax,ax
						$$B1el:
	d)	Флаговые сравнения

		if$	,nc,,	L			jnc	$$B1f
			mov	ax,3			jmp	$$B1el
		else$				$$B1f:
			mov	ax,6			mov	ax,3
		endif$					jmp	short $$B1e
						$$B1el:
							mov	ax,6
						$$B1e:
		Параметр L здесь значения не имеет,приводя к гене-
		рации лишней команды - указан для подчеркивания
		необходимости запятых.
				    _5_

		3. Макроопределение SWITCH
Синтаксис:

	SWITCH	reg,<p1,p2,p3,p4,.....,pn>	[L]

	CASE	1
		[ последовательность операторов 1 ]
		[BREAK [L] ]

	CASE	2
		[ последовательность операторов 2 ]
		[BREAK [L] ]

	CASE	3
		[ последовательность операторов 3 ]
		[BREAK [L] ]
		.				.
		.				.
		.				.

	CASE	N
		[ последовательность операторов N ]

		endcase

Параметры:	1. reg - рабочий регистр

		2. p1,p2,p3,...pn  - параметры (числа,символы
		   регистры) для сравнения с рабочим регистром.

		3. Каждому из  p1 - pn соответствует CASE,куда
		   будет передано управление, в случае равенства
		   параметра и рабочего регистра. Если после
		   выполнения оператора данного CASE, управление
		   должно быть передано на endcase ,то поставьте
		   необязательный BREAK.

		4. L - при наличии этого параметра происходит
		   формирование jmp (длинного) перехода.
		   Допустим любой символ .

Описание:	Сравнение происходит с помощью команды CMP.
		При наличии параметра L  формируется JMP переход.
Пример:
		xor	ax,ax			;ввод символа с клавиатуры
		int	16h

		SWITCH	al,<27,26,03>

		case	1
			mov	escf  ,1	;установить флаг ESC
			break
		case	2
			mov	ctrl_z,1	;установить флаг CTRL-Z
			break
		case	3
			mov	ctrl_c,1	;установить флаг CTRL-C
		endcase


				    _6_

		   Макроопределение SWITCH   (продолжение)

Листинг:		xor	ax,ax
			int	16h
			cmp	al,27
			jz	$$A11c
			cmp	al,26
			jz	$$A12c
			cmp	al,03
			jz	$$A13c
			jmp	short	$$A1e
		$$A11c:
			mov	esc_f,1
			jmp	short	$$A1e
		$$A12c:
			mov	ctrl_z,1
			jmp	short	$$A1e
		$$A13c:
			mov	ctrl_c,1
		$$A1e:

		Если при ассемблировании в строке SWITCH ........
		будет указана ошибка длины перехода
		   Jump out of range by N byte(s)
		то укажите параметр L для формирования JMP перехода.

		SWITCH	al,<27,26,03>		L

		case	1
			mov	escf  ,1	;установить флаг ESC
			break
		case	2
			mov	ctrl_z,1	;установить флаг CTRL-Z
			break
		case	3
			mov	ctrl_c,1	;установить флаг CTRL-C
		endcase

Листинг:		cmp	al,27
			jnz	$$A11f
			jmp	$$A11c
		$$A11f: cmp	al,26
			jnz	$$A12f
			jmp	$$A12c
		$$A12f: cmp	al,03
			jnz	$$A13f
			jmp	$$A13c
		$$A13f: jmp	$$A1e
		$$A11c:
			mov	esc_f,1
			jmp	short	$$A1e
		$$A12c:
			mov	ctrl_z,1
			jmp	short	$$A1e
		$$A13c:
			mov	ctrl_c,1
		$$A1e:
	То же самое относится и к оператору  BREAK.

				     _7_

		4. Макроопределение DO

		4.1 Конструкция DO  ENDDO

Синтаксис:

	DO	count

		[ последовательность операторов ]

	ENDDO	[L]

Параметры:	count - Число повторений цикла
		L     - Указатель для формирования перехода длиннее 127 байт

Описание:	Формируется loop цикл при отсутствии параметра L
		При наличии параметра L  формируется JMP переход.
		Для счетчика используется регистр CX, причем
		счет ведется в убывающем порядке !!!.
Пояснения:
		1. [ ] - необязательный параметр

		2. Не указывайте L пока при ассемблировании не

		   появится сообщение о превышении длины перехода:

		   Jump out of range by N byte(s)
		после чего в строке,помеченной ошибкой добавьте L.

Пример:     a)	mov	ax,0			  b)	mov	ax,0
		DO	10				DO	10
		add	ax,cx				add	ax,cx
		ENDDO					ENDDO	L

		Вычисляется сумма натурального ряда чисел
		от 10 до 1

Листинг:    a)					  b)
			mov	ax,0				mov	ax,0
			DO	10				DO	10
			push	cx				push	cx
			mov	cx,10				mov	cx,10
		$$A2n:					$$A1n:
			add	ax,cx				add	ax,cx
			ENDDO					ENDDO	 L
			loop	$$A2n				dec	cx
		$$A2e:						jz	$$A1e
			pop	cx				jmp	$$A1n
							$$A1e:
								pop	cx



				     _8_

		4.2 Конструкция DO  UNTIL
 Синтаксис:
		DO
		[ CONTINUE [L] ]
		[ последовательность операторов ]
		[ BREAK  [L] ]
		UNTIL	reg rel arg [L]
Параметры:
		1. reg - рабочий регистр
		2. rel - отношение сравнения,получающееся
		   отбрасыванием буквы J в соответствующей
		   команде перехода (LE E NB NGE и.т.д)
		3. arg - число (символ,регистр) для сравнения
		4. L - при наличии этого параметра происходит
		   формирование jmp (длинного) перехода.
		   Допустим любой символ .
Описание:
		Сравнение происходит с помощью команды CMP.
		Передача управления на начало цикла происхо-
		дит при невыполнении заданного условия. Приме-
		нение BREAK и CONTINUE аналогично и описано
		в макрокоманде FOR  ENDFOR.
Пример: 	Ожидание ввода определенного символа с клавиатуры

	a)	DO				     DO
		xor	ax,ax		      $$A5n:
		int	16h			     xor     ax,ax
		UNTIL	al  e 'R'                    int     16h
						     UNTIL   al e 'R'
							   cmp	   al,'R'
							   jne	  $$A5n
					      $$A5e:
	b)	DO				     DO
		xor	ax,ax		      $$A6n:
		int	16h			     xor     ax,ax
		UNTIL	al  e 'R' Long               int     16h
						     UNTIL   al e 'R' Long
							   cmp	   al,'R'
							   je	   $$A6e
							   jmp	    $$A6n
					       $$A6e:
	c)	Ввод символа ,имеющего код менее 80h.Так как параметры
		reg и arg обязательны,то необходимо сохранять запятые.
		Параметр long  здесь не нужен вызывая генерацию лишней
		команды и приведен для подчеркивания необходимости
		запятых. Листинг показывает,чтов этом случае нет
		команды сравнения.
		Проверка условия происходит после выполнения цикла.
		DO				$$A1n:
		xor	ax,ax				xor	ax,ax
		int	16h				int	16h
		or	al,al				or	al,al
		UNTIL	,s,,long			js	$$A1e
							jmp	$$A1n
						$$A1e:
Примечание:	Предпочтительно использование (во избежание ошибок
		при отрицании условия):
		JP  вместо JPE		и      JNP вместо JPO
		Не используйте "нестандартное" JCXZ

				     _9_

		5. Конструкция	 FOR  ENDFOR
 Синтаксис:
		FOR	reg n1 n2 step	 [L]
		[ CONTINUE [L] ]
		[ последовательность операторов ]
		[ BREAK [L] ]
		ENDFOR	[L]
Параметры:
		1. reg - рабочий регистр
		2. n1- начальное значение регистра reg
		3. n2- конечное  значение регистра reg
		4. step- шаг приращения регистра reg
		5. L - при наличии этого параметра происходит
		   формирование jmp (длинного) перехода.
		   Допустим любой символ .
Описание:
		Сравнение происходит с помощью команды CMP.
		Прекращение выполнения цикла происходит при
		совпадении значений регистра и n2. По окон-
		чании цикла рабочий регистр содержит значе-
		ние равное n2. Цикл выполняется после
		проверки условия !!!
Пример:
		FOR	CX 2 100 2

;		[ Последовательность операторов ]

		ENDFOR
Листинг:
			 mov	 CX,2
			 sub	 CX,2
		 $$A1n:
			 cmp	 CX,100
			 jz	$$A1e
			 add	 CX,2
		 ;		 [ Последовательность операторов ]
			 jmp	 short $$A1n
		 $$A1e:

		FOR	CX 2 100 2	L

;		[ Последовательность операторов ]

		ENDFOR			L

Листинг:		 mov	 CX,2
			 sub	 CX,2
		 $$A2n:
			 cmp	 CX,100
			 jnz	 $$A2f
			 jmp	 $$A2e
		 $$A2f:
			 add	 CX,2

		 ;		 [ Последовательность операторов ]

			 jmp	 $$A2n
		 $$A2e:
				       _10_


		Макрокоманда FOR   ENDFOR	(продолжение)

		Использование макрокоманд	CONNINUE и BREAK


	CONTINUE - вызывает переход к следующей итерации цикла без
		   завершения предыдущей

	BREAK	 - вызывает немедленный выход из цикла

Пример:
	xor	ax,ax
	lea	bx,TABL

	FOR	di 0 100 5		;<-------
					;	 !
	mov	cx,[bx+di]		;	 !
					;	 !
		if$	cx e 0		;	 !
		break	       ;----		 !
		endif$	       ;    !		 !
			       ;    !		 !
		if$	,s,,   ;    !		 !
		continue       ;    ! -----------
		endif$	       ;    !
			       ;    !
	add	ax,cx	       ;    !
			       ;    !
	ENDFOR		       ;<---

Листинг:
		mov	di,0
		sub	di,5
	$$A1n:
		cmp	di,100
		jz     $$A1e
		add	di,5

		mov	cx,[bx+di]

		cmp	cx,0
		jne    $$B2el
		jmp	short	$$A1e
	$$B2el:
		jns    $$B3el
		jmp	short	$$A1n
	$$B3el:

		add	ax,cx
		jmp	short $$A1n
	$$A1e:






				      _11_


		6.  Конструкция WHILE

 Синтаксис:
		WHILE	reg rel arg [L]
		[ CONTINUE [L] ]
		[ последовательность операторов ]
		[ BREAK    [L] ]
		ENDWH	[L]

Параметры:
		1. reg - рабочий регистр

		2. rel - отношение сравнения,получающееся
		   отбрасыванием буквы J в соответствующей
		   команде перехода (LE E NB NGE и.т.д)
		3. arg - число (символ,регистр) для сравнения

		4. L - при наличии этого параметра происходит
		   формирование jmp (длинного) перехода.
		   Допустим любой символ .
Описание:
		Сравнение происходит с помощью команды CMP.
		Выполнение цикла происходит при выполнении
		заданного условия, выход из цикла при не-
		выполнении. В отличие от DO  UNTIL условие
		проверяется перед выполнением цикла !!!


	Автор предлагает читателю самому придумать пример

	использования данного макроса.
				    _12_


		7. Конструкция	CYCLE  CYCLEEND


Синтаксис:
		CYCLE

		[ последовательность операторов ]

		[ CONTINUE [L] ]

		[ последовательность операторов ]

		[ BREAK    [L] ]

		. . . . . . . . . . . . . . . . .

		CYCLEEND


Описание:	Макрокоманда CYCLE определяет метку для перехода
		формируемого макрокомандой CONTINUE.

		Макрокоманда CYCLEEND определяет метку для перехода
		формируемого макрокомандой BREAK.

		Каждой CYCLE должна соответствовать CYCLEEND.


Пример:
	  a)	Иная форма оператора SWITCH (как в языке C )
		но с возможностью множественного сравнения.

		CYCLE

			IF$ AX E 12

				   [ последовательность операторов ]

				   ENDIF$
			BREAK

			IF$ BX E 13

				   [ последовательность операторов ]

				   ENDIF$
			BREAK

			IF$ VAR E 27

				   [ последовательность операторов ]

				   ENDIF$
		CYCLEEND

				   _13_


	b)	Организация IF циклов.

		1) Аналог цикла UNTIL но с операцией OR для отношений.

		CYCLE			    или   CYCLE


		[ последов. операторов ]	  [ последов. операторов ]


		IF$	reg rel arg		  IF$	  reg not.rel arg
		 BREAK				   CONTINUE
		ENDIF$				  ENDIF$
		IF$	reg rel1 arg		  IF$	  reg not.rel1 arg
		 BREAK				   CONTINUE
		ENDIF$				  ENDIF$
		CONTINUE

		CYCLEEND			  CYCLEEND

		2) Аналог цикла WHILE но с операцией AND для отношений.
		   Отношение должно быть противоположным так как выход
		   из цикла происходит при выполнении условий.

		CYCLE
		IF$	reg not.rel arg
		 IF$	 reg1 not.rel1 arg1
		  BREAK
		 ENDIF$
		ENDIF$

		[ последовательность операторов ]

		CYCLEEND

				      _14_

		7. Вложения макрокоманд.

	Количество вложений макрокоманд  не ограничивается  !!!
	Ограничено лишь общее число макрокоманд типа begin
	( FOR,DO,SWITCH,WHILE )  числом 65535, которое является
	наибольшим целым без знака,помещающимся в 2 байта.
Пример, демонстрирующий возможности
	вложения макрокоманд.

	DO
	xor	ax,ax
	int	16h		; ввод символа с клавиатуры

		SWITCH	al,<'A','B','C','D',0>
		case	1
			call	sub1
			break
		case	2
			call	sub2
			break
		case	3
			call	sub3
			break
		case	4
			call	sub4
			break
		case	5
			switch	ah,<104,105,106>	 ; анализ EXTENDED кодов
			case	1			 ; ALT-F1,ALT-F2,ALT-F3
			call	sub5
			break
			case	2
			call	sub6
			break
			case	3
			call	sub7
			endcase
		endcase
	UNTIL	al  e 27	; ESC
Листинг:
$$A1n:
	xor	ax,ax
	int	16h		; ввод символа с клавиатуры
	cmp	al,'A'
	jz	$$A21c
	cmp	al,'B'
	jz	$$A22c
	cmp	al,'C'
	jz	$$A23c
	cmp	al,'D'
	jz	$$A24c
	cmp	al,0
	jz	$$A25c
	jmp	short	$$A2e
$$A21c:
			call	sub1
	jmp	short	$$A2e
$$A22c:
			call	sub2
	jmp	short	$$A2e

				     _15_

		Вложения макрокоманд  ( продолжение )
$$A23c:
			call	sub3
	jmp	short	$$A2e
$$A24c:
			call	sub4
	jmp	short	$$A2e
$$A25c:
				       ; анализ EXTENDED кодов
	cmp	ah,104
	jz	$$A31c
	cmp	ah,105
	jz	$$A32c
	cmp	ah,106
	jz	$$A33c
	jmp	short	$$A3e
$$A31c:
			call	sub5
	jmp	short	$$A3e
$$A32c:
			call	sub6
	jmp	short	$$A3e
$$A33c:
			call	sub7
$$A3e:
$$A2e:
				; ESC
	cmp	al,27
	jne	$$A1n
$$A1e:

				     _16_


		9. Ошибки.

	К основным ошибкам относятся :


	1) Использование ENDIF,IF,ELSE	вместо ENDIF$,IF$,ELSE$,соответственно.

	2) Несоответствие числа макрокоманд типа begin	и end.

	3) Пропуск необходимых запятых при флаговых проверках.

	4) Несоответствие числа значений и числа случаев (CASE)
	   в конструкции SWITCH.

	 На большинство ошибок макроассемблер выдает сообщение

	  - как правило это неопределенные метки.

				     _17_


			  П Р И Л О Ж Е Н И Я


	Приложение 1.

	Возможные отношения для использования в макрокомандах

	UNTIL,WHILE,IF$ :

	JA     -->	A
	JAE    -->	AE
	JB     -->	B
	JBE    -->	BE
	JC     -->	C
	JE     -->	E
	JG     -->	G
	JGE    -->	GE
	JL     -->	L
	JLE    -->	LE
	JNA    -->	NA
	JNAE   -->	NAE
	JNB    -->	NB
	JNBE   -->	NBE
	JNC    -->	NC
	JNE    -->	NE
	JNG    -->	NG
	JNGE   -->	NGE
	JNL    -->	NL
	JNLE   -->	NLE
	JNO    -->	NO
	JNP    -->	NP
	JNS    -->	NS
	JNZ    -->	NZ
	JO     -->	O
	JP     -->	P
	JS     -->	S
	JZ     -->	Z

	Приложение 2.

;12-01-90 Yuri Kozlovski ,Izmail
;
;4_________ Structure programming macroses _________
;

;	General macroses
use__1	macro	z
lev__&z =0
	endm

				     _18_


	      10.1 Макроопределение IF$OR
Синтаксис:

	IF$OR	<reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

		  [ последовательность операторов 1 ]

	ELSE$
		[ последовательность операторов 2 ]
	ENDIF$

Пример:
       IF$OR <ax,cx,dh>,<e,a,ne>,<1,2,3>

		  [ последовательность операторов 1 ]	;Выполняется при
							; ax e 1     или
							; cx a 2     или
							; ax ne 3
	ELSE$
		[ последовательность операторов 2 ]
	ENDIF$


Листинг:

       IF$OR  <ax,cx,dh>,<e,a,ne>,<1,2,3>
	      cmp     ax,1
	je     $$B1s
	      cmp     cx,2
	ja     $$B1s
	      cmp     dh,3
	jne	$$B1s
	jmp   short $$B1el
$$B1s:

		  [ последовательность операторов 1 ]	  ;Выполняется при
							  ; ax e 1     или
							  ; cx a 2     или
							  ; ax ne 3
	ELSE$
	jmp	short $$B1e
$$B1el:
		  [ последовательность операторов 2 ]
	ENDIF$
$$B1e:
;




				     _19_


	      10.1 Макроопределение IF$AND
Синтаксис:

	IF$AND	<reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

		  [ последовательность операторов 1 ]

	ELSE$
		[ последовательность операторов 2 ]
	ENDIF$

Пример:
       IF$AND <ax,cx,dh>,<e,a,ne>,<1,2,3>

		  [ последовательность операторов 1 ]	;Выполняется при
							; ax e 1     и
							; cx a 2     и
							; ax ne 3


	ELSE$
		[ последовательность операторов 2 ]
	ENDIF$


Листинг:

       IF$AND  <ax,cx,dh>,<e,a,ne>,<1,2,3>
	      cmp    ax,1
	jne    $$B2el
	      cmp    cx,2
	jna    $$B2el
	      cmp    dh,3
	je  $$B2el
		  [ последовательность операторов 1 ]	;Выполняется при
							; ax e 1     и
							; cx a 2     и
							; ax ne 3


	ELSE$
	jmp	short $$B2e
$$B2el:
		[ последовательность операторов2 ]
	ENDIF$


				     _20_

		10.3  Макроопределение	 WHILE$OR

Синтаксис:

    WHILE$OR  <reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

    [CONTINUE]
		  [ последовательность операторов   ]
    [BREAK]

    ENDWH

		10.4  Макроопределение	 WHILE$AND

Синтаксис:

    WHILE$AND <reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

    [CONTINUE]
		  [ последовательность операторов   ]
    [BREAK]

    ENDWH

		10.5  Макроопределение	 UNTIL$OR
Синтаксис:

    DO

    [CONTINUE]
		  [ последовательность операторов   ]
    [BREAK]

    UNTIL$OR  <reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

		10.6  Макроопределение	 UNTIL$AND
Синтаксис:

    DO

    [CONTINUE]
		  [ последовательность операторов   ]
    [BREAK]

    UNTIL$AND <reg1,reg2,..,regN>,<rel1,rel2,..,relN>,<arg1,arg2,..,argN> [L]

