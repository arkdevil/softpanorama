

		Г Е Н Е Р А Ц И Я      С И М В О Л О В

	Э К Р А Н А     Д Л Я     А Д А П Т Е Р О В     E G A / V G A


			Алекс. Ф. О. Эвелгартен


                Просматривая многочисленные описания видеорежимов на
        русском языке, я  с  удивлением  обнаружил,  что  совершенно
        обойдена вниманием проблема генерации символов. 
                Постоянно сталкиваясь с необходимостью изменения оп-
        ределенных символов экрана, я постарался изложить  здесь ос-
        новную информацию, необходимую для  использования знакогене-
        ратора для текстовых режимов адаптеров EGA/VGA. 

                Структуры символов (фонты) хранятся в  особой плоск-
        ости видеопамяти, и доступны для прямого чтения в определен-
        ном режиме.
                BIOS хранит в себе  несколько  таблиц  символов, ис-
        пользуемых для первоначальной генерации символов при инициа-
        лизации PC после включения питания.

                Вектор 1Dh содержит адрес таблицы параметров для ре-
        жимов поддерживаемых CGA.
                По адресу 0:4a8h содержится адрес таблицы параметров
        для режимов поддерживаемых EGA/VGA.
                По адресу 0F000h:0FA6Eh  содержится  таблица  фонтов
        символов для режимов CGA (8x8) (первые 128 символов  -  коды
        0-7Fh).
                Вектор 1Fh содержит адрес таблицы фонтов для второго
        набора символов (8x8 коды 80h-0ffh).
                Вектор 43h содержит адрес таблицы фонтов для различ-
        ных размеров символов (от 8x8, 8x14 до 8x16)

                В алфавитно-цифровом режиме  видеобуфер представляет
        из себя четыре параллельные плоскости (карты) как и в графи-
        ческом режиме. но лишь две из них участвуют в создании изоб-
        ражения. Четные байты адресуются картой  0  и  содержат  код
        символа, нечетные - картой 1 и содержат атрибут символа.

                           ┌───────────────────────┐
                           │                       │
                           │                       │
                       ┌───┴───────────────────┐   │
                       │ Область определения   │   │
                       │      символов         │   │
                   ┌───┴───────────────────┐   │   │
                   │    Байты атрибутов    │   │   │
                   │                       │   │   │
               ┌───┴───────────────────┐   │   │   │
               │    Коды символов      │   │   │   │
               │                       │   │   ├───┘ Карта 3
               │                       │   │   │
               │                       │   │   │
               │                       │   ├───┘ Карта 2
               │                       │   │
               │                       │   │
               │                       ├───┘ Карта 1
     B800:0000 │                       │
        или    │                       │
     B000:0000 └───────────────────────┘ Карта 0    


                Генератор символов экрана использует  несколько таб-
        лиц, размещаемых во 2 карте. Каждая таблица состоит  из  256
        элементов по 32 (20H) байта в каждом,  причем  фонт  символа
        размещается в младших байтах, а старшие не используются. 

                В EGA существует четыре набора символов,  каждый  из
        которых начинается на 16 Кб границе в  видеопамяти. (Символы
        занимают первые 8 Кб, остальные 8 Кб не используются.) В VGA
        присутствуют 8 наборов символов, начинающихся с 8 Кб границу
        и занимающих всю отведенную память.

        EGA :      ┌─────────────────────────────┐
                   │     (Не используется)       │
                   ├─────────────────────────────┤
                   │       Набор символов        │
             C000H ├─────────────────────────────┤
                   │     (Не используется)       │
                   ├─────────────────────────────┤
                   │       Набор символов        │
    Смещение 8000H ├─────────────────────────────┤
                   │     (Не используется)       │
                   ├─────────────────────────────┤
                   │       Набор символов        │
             4000H ├─────────────────────────────┤
                   │     (Не используется)       │
                   ├─────────────────────────────┤
                   │       Набор символов        │
             0000H └─────────────────────────────┘

     VGA :         ┌─────────────────────────────┐
                   │       Набор символов        │
             E000H ├─────────────────────────────┤
                   │       Набор символов        │
             C000H ├─────────────────────────────┤
                   │       Набор символов        │
             A000H ├─────────────────────────────┤
                   │       Набор символов        │
    Смещение 8000H ├─────────────────────────────┤
                   │       Набор символов        │
             6000H ├─────────────────────────────┤
                   │       Набор символов        │
             4000H ├─────────────────────────────┤
                   │       Набор символов        │
             2000H ├─────────────────────────────┤
                   │       Набор символов        │
             0000H └─────────────────────────────┘

                Собственно говоря, генерировать символы можно  и при
        помощи функции BIOS-а (Int 10h,  Ah=11h),  как  и  поступают
        большинство драйверов, обращаясь к данной функции после уст-
        ановки режима. Но вести подобным образов  генерацию символов
        отдельно от  установки  режима  неудобно,  так  как  функция
        BIOS-а делает слишком много дополнительных установок регист-
        ров адаптера, из-за чего экран ощутимо  дергается. Указанная
        ниже последовательность операций с портами лишена  этого до-
        садного недостатка.

╔════════════════════════════════════════════════════════════════════════════╗
║        Последовательность изменения состояния регистров EGA/VGA            ║
╠══════╤══════════════════════════════════╤══════════════════════════════════╣
║ Порт │ Для включения генерации символов │  При завершении генерации        ║
╠══════╪══════════════════════════════════╧══════════════════════════════════╣
║ 3c4h │ Код 2h - выбор регистра маски карты                                 ║
╟──────┼──────────────────────────────────┬──────────────────────────────────╢
║ 3c5h │ Код 4h - 0000 0100               │ Код 3h - 0000 0011               ║
║      │          выбор плоскости 2 для   │          выбор плоскости 0 и 1   ║
║      │       генерации символов         │       для обычной работы         ║
╠══════╪══════════════════════════════════╧══════════════════════════════════╣
║ 3c4h │ Код 4h - выбор регистра режима памяти                               ║
╟──────┼──────────────────────────────────┬──────────────────────────────────╢
║ 3c5h │ Код 7h - 0000 0111               │ Код 3h - 0000 0011               ║
║      │          разрешение для процес-  │          отключение, установлен- ║
║      │       сора вести запись в плос-  │       ного в начале генерации,   ║
║      │       кость определенную в       │       режима записи; разрешение  ║
║      │       в предыдущем регистре      │       процессору вести запись    ║
║      │                                  │       в плоскости 0 для четных   ║
║      │                                  │       адресов и 1 для нечетных   ║
╠══════╪══════════════════════════════════╧══════════════════════════════════╣
║ 3ceh │ Код 4h - выбор регистра маски карты                                 ║
╟──────┼──────────────────────────────────┬──────────────────────────────────╢
║ 3cfh │ Код 2h - 0000 0010               │ Код 0h - 0000 0000               ║
║      │          выбор плоскости видео   │          выбор плоскости видео   ║
║      │       памяти для чтения процес-  │       памяти для чтения процес-  ║
║      │       сора (0-7), здесь 2        │       сора (0-7), здесь 0        ║
╠══════╪══════════════════════════════════╧══════════════════════════════════╣
║ 3ceh │ Код 5h - выбор регистра режима записи                               ║
╟──────┼──────────────────────────────────┬──────────────────────────────────╢
║ 3cfh │ Код 0h - 0000 0000               │ Код 10h - 0001 0000              ║
║      │               биты:              │           выборка производится   ║
║      │          0-1 режим записи        │        согласно четности адреса  ║
║      │              0 - вращение данных │                                  ║
║      │          2   не используется     │                                  ║
║      │          3   режим чтения        │                                  ║
║      │              0 - процессор читает│                                  ║
║      │       данные из текущей плоскости│                                  ║
║      │          4   четность плоскости  │                                  ║
║      │              0 - выбор регистра  │                                  ║
║      │       контроля плоскостей        │                                  ║
║      │          5   включение регистра  │                                  ║
║      │       задвижки                   │                                  ║
║      │          6-7 не используется     │                                  ║
╠══════╪══════════════════════════════════╧══════════════════════════════════╣
║ 3ceh │ Код 6h - выбор регистра дополнительных операций                     ║
╟──────┼──────────────────────────────────┬──────────────────────────────────╢
║ 3cfh │ Код 4h - 0000 0100               │ Код 0eh - 0000 1110              ║
║      │               выбор буфера 0A000h│           выбор буфера 0B800h    ║
║      │               загрузка генератора│           прекращение работы     ║
║      │                                  │           ( VGA,EGA,CGA дисплеи) ║
║      │                                  │                                  ║
║      │                                  │ Код 0аh - 0000 1010              ║
║      │                                  │           выбор буфера 0B000h    ║
║      │                                  │           прекращение работы     ║
║      │                                  │           ( MDA,HDA дисплеи)     ║
╚══════╧══════════════════════════════════╧══════════════════════════════════╝

	        Пример программы, реализующей генерацию символов :

                Пример представляет из себя программу Smile на языке
        Ассемблера, которая каждые полторы секунды попеременно пере-
        ворачивающая символы  относительно  горизонтальной  и верти-
        кальной осей. Для  размещения  тела  программы  используется
        первое после Command.com PSP (предполагается, что оно не ис-
        пользуется другими программами). Программа  приведена  и от-
        дельно в файле Smile.asm, ее нужно оттранслировать Турбо-Ас-
        семблером 2.0 и отредактировать для создания COM-файла.

		Title		Evelgarten EGA\VGA symbol generation

		Page	64,

Smile		Segment	Byte Public

Br		Equ	Byte Ptr
Wr		Equ	Word Ptr
Of		Equ	Offset
Inpsp		Equ	5ch

Movtt		Macro	A,B

		Push	B
		Pop	A

		Endm

		Assume	Cs: Smile, Ds: Smile

		Org	100h
Start:
		Jmp	Ito
Int8:
		Push	Ds Ax Bx Cx Dx Si Di

		Movtt	Ds,Cs
		Mov	Si,(Of Tau - Of Int8) + Inpsp

		Dec	Ds: Br [Si]
		Jnz	Outint8

		Cli				; Не обязательно, но
						; следует использовать
		Cld
		Xor	Cx,Cx
		Mov	Br [Si],20h
		Inc	Si

		Call	En_set			; Разрешение генерации

		Mov	Ds,Cx

		Mov	Dx,Ds: [485h]		; Размер символа
		Dec	Dx			; из области BIOS-а

		Jmp	Short Cik
						; Для PSP, область параметра
		Db	0,0dh			; В программе смещение 27h
Cik:
		Xor	Si,Si			; Начальное смещение
		Mov	Ch,0a0h			; Графическая область,
		Mov	Ds,Cx			; используемая для генерации

		Mov	Ch,1			; Один набор (256 символов),
Ci:						; а вообще-то их до 8
		Mov	Bx,Si
		Mov	Di,Si			; Начало символа
		Add	Di,Dx			; Конец символа
Sy:
		Lodsb
Simo:
		Call	Sink			; Переворачивание символа

		Cmp	Si,Di			; Конец символа ?
		Jbe	Sy

		Mov	Si,Bx
		Add	Si,20h			; Следующий символ

		Loop	Ci

		Movtt	Ds,Cs
		Mov	Si,(Of Set2 - Of Int8) + Inpsp

		Call	En_set			; Завершение генерации

		Xor	Br Ds: (Of Simo+1 - Of Int8) + Inpsp,2

		Sti
Outint8:
		Pop	Di Si Dx Cx Bx Ax Ds

		Db	0eah
Oldint8		Dd	0

Tau		Db	20h

Set1		Db	2,4			; Параметры включения
		Db	4,7			; генерации
		Db	4,2
		Db	5,0
		Db	6,4

Set2		Db	2,3			; Параметры завершения
		Db	4,3			; генерации
		Db	4,0
		Db	5,10h
		Db	6

Set3		Db	0eh			; Зависит от типа дисплея

En_set:
		Mov	Cl,2
		Mov	Dx,3c4h

		Call	Porty

		Mov	Cl,3
		Mov	Dl,0ceh
Porty:
;		Lodsb				; I метод
;                                       
;		Out	Dx,Al           
;
;		Inc	Dx			; Эти два  куска  текста
;                                               ; аналогичны и представ-
;		Lodsb                           ; ляют два различных ме-
;                                               ; тода общения с портами
;		Out	Dx,Al
;
;		Dec	Dx

		Lodsw				; II метод

		Out	Dx,Ax

		Loop	Porty

		Retn
Sim1:
		Push	Cx			; Вращение символа
						; относительно
		Mov	Cl,8			; вертикальной оси
T:
		Rcr	Al,1
		Rcl	Ah,1

		Loop	T

		Pop	Cx

		Mov	[Si-1],Ah

		Retn
Sim2:
		Xchg	Al,[Di]			; Вращение символа
		Mov	[Si-1],Al		; относительно
						; горизонтальной оси
		Dec	Di

		Retn

		Jmp	Short Sim1	       ; Разветвление
Sink:
		Jmp	Short Sim2

		Jmp	Short Sim1             ; Страховка при трансляции

Lint8		Equ	$ - Of Int8

Kito:
		Mov	Ax,Ds
		Inc	Ax
		Add	Ax,Ds: [3]

		Retn
Ito:
		Xor	Ax,Ax
		Mov	Ds,Ax

		Mov	Al,Ds: [487h]		; 0 - EGA/VGA отсутствует

		Cmp	Al,0
		Je	Nega

		Test	Al,2

		Movtt	Ds,Cs

		Jz	Yega

		Mov	Br Set3,0ah		; Монохромный дисплей
Yega:
		Mov	Ax,3508h
		Int	21h

		Mov	Wr Oldint8,Bx
		Mov	Wr Oldint8+2,Es

		Mov	Ah,52h
		Int	21h

		Mov	Ds,Es: [Bx-2]

		Call	Kito
Ito0:
		Mov	Ds,Ax

		Call	Kito

		Cmp	Wr Ds: [1],50h
		Jbe	Ito0

		Mov	Dx,Ax
		Inc	Dx
Ito1:
		Mov	Ds,Ax

		Call	Kito

		Mov	Bx,Ds: [1]

		Cmp	Dx,Bx
		Jae	Ito1

		Mov	Es,Bx
		Movtt	Ds,Cs

		Cld
		Mov	Di,Inpsp
		Mov	Dx,Di
		Mov	Si,Of Int8
		Mov	Cx,Lint8

		Rep	Movsb

		Movtt	Ds,Es

		Mov	Ax,2508h
		Int	21h
Nega:
		Mov	Ax,4c00h
		Int	21h

Smile		Ends

		End	Start


		При написании статьи использовались материалы :

			1.  RICHARD  WILTON  "PROGRAMMER'S  GUIDE TO
	PC(R)   AND   PS/2(TM)   VIDEO   SYSTEMS".   Maximum   Video
	Performance from the EGA(TM), VGA, HGC, and MCGA.

			2.  EG-3000  (A)  ENHANCED  GRAPHICS ADAPTER
	TECHNICAL MANUEL. Gemini Tecnology Inc.  


				Алекс. Ф. О. Эвелгартен
				(Алексей А. Сальников)

				620219 РСФСР г. Свердловск
				ул. К. Либкнехта 9.  
				Вычислительный центр СвГПИ.
				Телефоны (8-343-2) 51-52-55, 51-95-07
				Телефакс 221-212 "DIANA" Факс 511-015
