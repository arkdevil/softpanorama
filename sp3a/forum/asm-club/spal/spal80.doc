




                СТРУКТУРНЫЙ АССЕМБЛЕР (SPAL)
                    для процессора 8080


    Структурный асссемблер (SPAL) представляет собой библиотеку макро-
    определений, с помощью которой можно легко реализовать основные
    конструкции структурного программирования. В библиотеку SPAL
    входят следующие макроопределения :

             .AND      .OR       .BEGIN
             .IF       .ELSE     .ENDI
             .BREAK    .BREAK_IF
             .WHILE    .ENDW
             .DO       .ENDD
             .FOR      .ENDF
             .LOOP     .ENDL
	     .SWITCH   .CASE     .DEFAULT   .ENDS
	     .PUSH     .POP
	     .CALL     .ENTER    .LEAVE


      Для трансляции программы, написанной на языке SPAL80, необходимо
    включить в транслируемый модуль файл библиотеки SPAL80 и файл биб-
    лиотеки CMD80:

		   include cmd80.inc
		   include spal80.inc



            ОСНОВНЫЕ ПОНЯТИЯ И ОБОЗНАЧЕНИЯ


	  1.  Типы адресации и типы данных.
	  ----------------------------------
	   При генерации кодов SPAL80 использует библиотеку CMD80, поэтому
	типы адресации и типы данных такие же, как в этой библиотеке.
	Полный операнд определяется следующим образом :

		 <П_Опер> = <Тип_Адр Опер>

	  2.  Логическая операция.
          -------------------------
             Набор логических операций отвечает возможностям процессора 8080.
           Логические операции могут быть трех типов :
                 - битовые операции
                 - операции сравнения
                 - операции с флагами

	 Битовые операции :
	------------------------------
                 - ON   маска, заданная операндом 1 присутствует в
                        операнде 2.
                 - OFF  маска, заданная операндом 1 отсутствует в
                        операнде 2.

                 <Лог_Опер_Бит> = <ON | OFF>

	Когда логическая операция встречается в логическом выражении,
     генерируется команда and.

	Операции сравнения и операции с флагами:
       --------------------------------------------

       Эти операции имеют одинаковую мнемонику и отличаются количеством
     операндов в выражении:

	 1 операнд        - операция с флагами
	 2 или 3 операнда - операция сравнения

       Когда логическая операция интерпретируется как операция сравнения,
     генерируется команда cmp;
       когда логическая операция интерпретируется как операция с флагами,
     никаких команд для установки флагов не генерируется.


                 <Лог_Опер_Срав> = <Лог_Опер_Флаг> =

	 <A   | NBE | AE  | NB  | B   | NAE | BE  | NA  | E  | EQ  | NE |
	  G   | GT  | NLE | GE  | NL  | L   | LT  | NGE | LE | NG  |
	  NZ  | Z   | NS  | P   | S   | M   | NC  | C   | PO | PE
         >



	  3.  Логическое выражение.
          -------------------------
            В зависимости от количества операндов логическое выражение имеет
          следующий вид:

               -  0 операндов
               ---------------
                     <Лог_Выр>  =  <Лог_Опер_Флаг>
                  Логическая операция может быть только операцией с фла-
               гами.

                  Например :
                       NZ   -  флаг Z не установлен
                       PO   -  флаг P не установлен
                       M    -  флаг S установлен


               -  1 операнд
               -------------
                     <Лог_Выр>  =  <П_Опер  Лог_Опер_Срав>
                  Логическая операция может быть только операцией сравне-
               ния, а в качестве 2-го операнда подразумевается 0.

                  Например :
                      R b    EQ  -  содержимое регистра b равно 0
                      M idn  NE  -  содержимое ячейки  idn не равно 0
                      R m    GE  -  содержимое ячейки, адрес которой в hl
                                    больше или равно 0
                      RW b   LT  -  регистровая пара bc меньше 0


               -  2 операнда
               --------------
               <Лог_Выр>  =  <П_Опер1  Лог_Опер_Бит|Лог_Опер_Сра  П_Опер2>

                  Логическая операция может быть как битовой, так и опера-
	       цией сравнения.
		 Если логическая операция является операцией с битами,
	       генерируется команда
			       .TEST  <Опре1>,<Опер2>  из  CMD80
	       и соответствующая команда условного перехода.
		 Если логическая операция является операцией сравнения,
	       генерируется команда
			       .CMP   <Опер1>,<Опер2>  из  CMD80
	       и соответствующая команда условного перехода.

                  Например:
                     C  1     ON  R b    -  бит 0 установлен в регистре b
                     R  d     OFF R h    -  биты, установленные в регистре d,
                                            отсутствуют в регистре h
                     R  m     EQ  M  idn
                     RW b     EQ  IW <MW baza, C off>
                     MW word  GE  C  5




       Данная таблица раскрывает смысл операций с флагами и операций срав-
   нения в логических выражениях :
  ________________________________________________________________________
 !           !                           !  Смысл операции сравнения :    !
 ! Код опер. !   Состояние флагов        !    Опер 1 ....  Опер2 (или 0)  !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  A/NBE    !     (CF or  ZF) = 0       !  выше/не ниже и не равно       !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  AE/NB    !          CF = 0           !  выше или равно/не ниже        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  B/NAE    !          CF = 1           !  ниже/не выше и не равно       !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  BE/NA    !     (CF or ZF) = 1        !  ниже или равно/не выше        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  E/EQ     !          ZF = 1           !           равен                !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  NE       !          ZF = 0           !           не равен             !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  G/GT/NLE !     (SF or ZF) = 0        !  больше/не меньше и не равно   !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  GE/NL    !          SF = 0           !  больше или равно/не меньше    !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  L/LT/NGE !          SF = 1           !  меньше/не больше и не равно   !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  LE/NG    !     (SF or ZF) = 1        !  меньше или равно/не больше    !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  NZ       !         ZF = 0            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  Z        !         ZF = 1            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  NS/P     !         SF = 0            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  S/M      !         SF = 1            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  NC       !         CF = 0            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  C        !         CF = 1            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  PO       !         PF = 0            !          --------------        !
 !___________!___________________________!________________________________!
 !           !                           !                                !
 !  PE       !         PF = 1            !          --------------                      !
 !___________!___________________________!________________________________!


                                      Примечание :
                                         1. Термины "выше" и "ниже" применимы
                                            для сравнения беззнаковых величин
                                            (адресов).
                                         2. Термины "больше" и "меньше" ис-
                                            пользуются при учете знака числа.
                                         3. Слова xor и or обозначают соответ-
                                            ствующие логические операции.



            ОПЕРАТОРЫ СТРУКТУРНОГО АССЕМБЛЕРА


        Операторы, указанные в квадратных скобках, могут отсутствовать.



     1. Условный оператор .IF
     -------------------------

          .IF   <Лог_Выр>
        [ .OR   <Лог_Выр> ]
        [ .AND  <Лог_Выр> ]
          .BEGIN

            ...................

        [ .ELSE ]

            ...................

          .ENDI

       Примеры:

	 .IF R a  EQ        ;если значение аккумулятора равно 0
	 .OR RW b EQ C 1    ;или значение bc равно 1
         .BEGIN             ;то
            mvi  b,10       ;загрузить в b значение 10
         .ELSE              ;иначе
            mvi  b,20       ;загрузить в b значение 20
         .ENDI              ;конец оператора .IF

	 .IF   C 1 ON R b   ;если бит 0 в регистре b установлен
	 .AND  R m EQ R c   ;и содержимое по адресу hl равно регистру c
         .BEGIN             ;то
             .ADD M cnt, C 2    ;увеличить ячейку cnt на 2
         .ENDI              ;конец оператора .IF


	 .IF RW h EQ MW mem    ;если регистровой пара hl равна содержимому
	 .OR RW h EQ CW coor   ;слова по адресу mem или константе coor
	 .BEGIN
	    .INC RW h          ;увеличить содержимое регистровой пары hl
	 .ENDI




     2. Операторы выхода из цикла .BREAK и .BREAK_IF
     -----------------------------------------------

        Операторы выхода прерывают выполнение самого ближнего к этим опе-
     раторам цикла, который может быть любым из допустимых в языке SPAL80
     циклов. А именно :

            .WHILE  ------ .ENDW
            .DO     ------ .ENDD
            .FOR    ------ .ENDF
            .LOOP   ------ .ENDL

        Оператор .BREAK осуществляет безусловный выход из ближайшего цикла.
     Оператор .BREAK_IF имеет следующий вид:

             .BREAK_IF  <Лог_Выр>

     Если значение логического выражение истинно - выполняется выход из
     ближайшего цикла, если ложно - выполнение цикла продолжается.




     3. Оператор цикла .WHILE
     -------------------------

        .WHILE  <Лог_Выр>
      [ .OR     <Лог_Выр> ]
      [ .AND    <Лог_Выр> ]
        .BEGIN

          .....................

         [ .BREAK ]
         [ .BREAK_IF <Лог_Выр> ]

          .....................

        .ENDW

        Примеры :

         mvi  b, 0              ;  цикл повторяется пока b не станет
         .WHILE  R b NE         ;  равным 0 , но не более 100 раз
	 .BEGIN
	    .IF R c EQ C 100
	    .BEGIN
	       .BREAK
	    .ENDI
            dcr  b
	    inr  c
	 .ENDW

         С использованием оператора .BREAK_IF этот пример
      выглядит следующим образом :

         mvi c, 0
         .WHILE R b NE
         .BEGIN
	    .BREAK_IF R c EQ C 100
            dcr  b
            inr  c
         .ENDW

         Другой вариант реализации этого цикла :

         mvi c, 0
         .WHILE R b NE
	 .AND   R c NE C 100
         .BEGIN
            dcr  b
            inr  c
         .ENDW

	 С использованием операндов из двух байт:

	 .MOV  MW cnt, CW 0
	 .WHILE  MW limit NE
	 .AND    MW cnt   NE CW 500
	 .BEGIN
	    .DEC  MW limit
	    .INC  MW cnt
	 .ENDW




     4.  Оператор цикла DO
     ----------------------

        .DO

          .................

         [ .BREAK ]
         [ .BREAK_IF <Лог_Выр> ]

          .................

        .WHILE  <Лог_Выр>
      [ .OR     <Лог_Выр> ]
      [ .AND    <Лог_Выр> ]
        .ENDD

        Примеры :

          mvi  c, 0
          .DO
            dcr  b
            inr  c
	    .IF R c EQ C 100
            .BEGIN
               .BREAK
            .ENDI
          .WHILE R b NE
          .ENDD

        С использованием оператора .BREAK_IF этот пример выглядит
     следующим образом :

        mvi  c, 0
        .DO
           dcr  b
           inr  c
	   .BREAK_IF  R c EQ C 100
        .WHILE  R b NE
        .ENDD

        Другой вариант реализации этого цикла :

          mvi  c, 0
          .DO
             dcr  b
             inr  c
          .WHILE  R b NE
	  .AND    R c NE C 100
          .ENDD




     5. Оператор цикла .FOR
     -----------------------

             <счетчик>     = <П_Опер>
             <количество>  = <П_Опер>

        .FOR   <счетчик> [,<количество>]
        .BEGIN

           .................

         [ .BREAK ]
         [ .BREAK_IF <Лог_Выр> ]

           .................

        .ENDF

        В поле <счетчик> указывается переменная, которая используеся опера-
     тором .FOR для счетчика повторов. При нормальном выходе из цикла значе-
     ние этой переменной равно -1. В поле <количество> задается количество
     повторов. Если это поле не определено, считается что счетчик уже
     установлен ранее.

         Примеры :

             mvi  a, 0
	     .FOR  R b, C 100   ;  В 100 ячеек памяти записываетя 0
             .BEGIN
                sta  m
                inx  h
             .ENDF

        Другой вариант реализации :

            mvi  a, 0
            mvi  b, 100
            .FOR  R b
            .BEGIN
               sta  m
               inx  h
            .ENDF

	Если количество циклов больше чем 256, необходимо использовать
     в качестве счетчика операнд из двух байт:

	    mvi  a, 0
	    .FOR  RW b, CW 500
	    .BEGIN
	       sta  m
	       inx  h
	    .ENDF




     6.  Оператор цикла .LOOP
     -------------------------

         .LOOP

           .................

          [ .BREAK ]
          [ .BREAK_IF <Лог_Выр> ]

           .................

         .ENDL

         Примеры :

            mvi  a,0
            mvi  b,100
            .LOOP
               sta  m
               inx  h
               dcr  b
               .IF Z
               .BEGIN
                  .BREAK
               .ENDI
            .ENDL

         С исрользованием оператора .BREAK_IF этот пример выглядит
      следующим образом :

            mvi  a,0
            mvi  b,100
            .LOOP
               sta  m
               inx  h
               dcr  b
               .BREAK_IF Z
            .ENDL




     7.  Оператор выбора .SWITCH
     ----------------------------

                <переменная_выбора>      = <П_Опер>
                <конст1> = ... <конст5>  = <число | идентификатор>

         .SWITCH <переменная_выбора>
         .СASE   <конст_1>,[<конст_2>,<конст_3>,<конст_4>,<конст_5>]

                .............................

         .СASE   <конст_1>,[<конст_2>,<конст_3>,<конст_4>,<конст_5>]

                .............................

         .СASE   <конст_1>,[<конст_2>,<конст_3>,<конст_4>,<конст_5>]

                .............................

         ....................................
         ....................................

       [ .DEFAULT ]

                .............................

         .ENDS

        Пример :

             .SWITCH  R b
             .CASE    0,1
                           mvi  c,0
             .CASE    2,3
                           mvi  c,1
             .DEFAULT
                           mvi  c,2
             .ENDS




     8.  Оператор сохранения регистров в стеке и восстановление из
	 стека  .PUSH  .POP
     --------------------------------------------------------------

     .PUSH  [<П_Опер1>,<П_Опер2>,<П_Опер3>,<П_Опер4>,<П_Опер5>,<П_Опер6>]

              .............

     .POP   [<П_Опер1>,<П_Опер2>,<П_Опер3>,<П_Опер4>,<П_Опер5>,<П_Опер6>]

	Параметры, заданные в операторе .PUSH сохраняются в прямом порядке;
     параметры, заданные в операторе .POP восстанавливливаются в обрат-
     ном порядке.
	Если не указаны параметры в операторе .PUSH / .POP, то
     сохраняются/восстанавливаются PSW и все три регистровые пары.

	  .PUSH  RW b, RW h        ;сохраняются\восстанавливаются регист-
	  .POP   RW b, RW h        ;ровые пары bc и hl

	  .PUSH  MW idn, H <C 3>   ;сохраняются\восстанавливаются слово по
	  .POP   MW idn, H <C 3>   ;адресу idn и байт по адресу hl плюс 3

	  .PUSH RW b, RW h
	  mvi   a, 0
	  lhld  300H
	  .FOR R b, C 100
          .BEGIN
	     sta  m
	     inx  h
          .ENDF
	  .POP  RW b, RW h




     9. Оператор вызова процедур  .CALL
     -------------------------------------

   .CALL   name, <П_Опер1>,<П_Опер2>,<П_Опер3>,<П_Опер4>,<П_Опер5>,<П_Опер6>

      Записывет параметры в стек в обратном порядке, вызывает указанную
   процедуру. После возврата из процедуры восстанавливает стек. Максималь-
   ное количество параметров равно 6.

      Например:

	 .CALL  test  RW b, CW 5, MW idn

     При входе в процедуру параметр 1 будет иметь смещение 2 относительно
   указателя стека, параметр 2 - 4, и параметр 3 - 6. Смещение 0 относи-
   тельно указателя стека имеет адрес возврата.




     10. Операторы входа в процедуру и выхода из нее  .ENTER / .LEAVE
     ------------------------------------------------------------------

	.ENTER [<размер области локальных переменных>]
	-------

     Записывает значение указателя стека в регистровую пару hl. После
   этой этой команды к параметрам процедуры, переданным в команде .CALL
   можно обращаться следующим образом:

	       HW <C "смещение">,

   где "смещение" определяется как номер параметра в команде
   .CALL (начиная с 1) умножить на 2.

      Если определена область локальных переменных, указатель стека
   уменьшается на размер этой области. Локальные переменные имеют отрица-
   тельное смещение относительно регистровой пары hl



	.LEAVE
       ---------

      Восстанавливает указатель стека. !!! Внимание !!! Если в команде
   .ENTER была определена область локальных переменных, то перед выполнением
   команды .LEAVE значение регистровой пары hl должно быть таким же, как
   после команды .ENTER.

	  Например:

	     .CALL    proc  CW addr, RW d

;/////////////////////////////////////////////////////////////////////////
;            Процедура определяет количество ненулевых байт
;		в начале строки
;       Вызов:     .CALL  "адрес строки","размер строки"
;       Результат: be - количество ненулевых байт
;/////////////////////////////////////////////////////////////////////////
proc:
	.ENTER   2
	.CLR  HW <C -2>      ;Обнулить локальную переменную - счетчик
	.FOR  HW <C 4>       ;Использовать для счетчика параметр в стеке
	.BEGIN
	  .MOV  RW d, HW <C 2>
	  push  h
	  xchg
	  mov   a,  m
	  pop   h
	  .BREAK_IF  R a  EQ
	  .INC  HW <C -2>
	  .INC  HW <C  2>
	.ENDF
	.MOV  RW b, HW <C -2>
	.LEAVE
	ret

